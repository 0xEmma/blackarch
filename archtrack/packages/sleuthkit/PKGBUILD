# TODO: Add to package groups.
# old groups: archtrack archtrack-forensics
pkgname=sleuthkit-blackarch
_realname=sleuthkit
groups=(blackarch)
pkgver=4.0.2
pkgrel=1
provides=("sleuthkit=$pkgver")
conflicts=(sleuthkit)
pkgdesc='File system and media management forensic analysis tools'
arch=('i686' 'x86_64')
url='http://www.sleuthkit.org/sleuthkit'
license=('GPL2' 'CPL' 'custom')
depends=('perl')
options=('!libtool')
changelog=$_realname.changelog
source=(http://downloads.sourceforge.net/$_realname/$_realname-$pkgver.tar.gz)
sha256sums=('112f80fbc6a868c18c7a924f756b35c79eed9d0cc8923a4f973646f00e546e62')

build() {
  cd ${_realname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${_realname}-${pkgver}

  make DESTDIR=${pkgdir} install

  # licenses
  install -d ${pkgdir}/usr/share/licenses/${_realname}
  install -Dm0644 licenses/* ${pkgdir}/usr/share/licenses/${_realname}
}
