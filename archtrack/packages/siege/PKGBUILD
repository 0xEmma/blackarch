# TODO: Add to package groups.
# old groups: archtrack archtrack-stress-testing
pkgname=siege-blackarch
_realname=siege
groups=(blackarch)
pkgver=3.0.0
pkgrel=1
provides=("siege=$pkgver")
conflicts=(siege)
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc='An http regression testing and benchmarking utility'
url="http://www.joedog.org/JoeDog/Siege"
depends=('openssl')
options=(!libtool)
source=("http://www.joedog.org/pub/siege/siege-${pkgver}.tar.gz")
md5sums=('e0ace61f7e01c6b545ac767fc499f817')
backup=(etc/siegerc etc/urls.txt)

build() {
  cd "${srcdir}/$_realname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/$_realname-$pkgver"

  install -d -m755 "$pkgdir/etc"
  install -d -m755 "$pkgdir/usr/share"
  make DESTDIR="$pkgdir" install
  sed -i 's/^# file =$/file = \/etc\/urls.txt/' "$pkgdir/etc/siegerc"
}
