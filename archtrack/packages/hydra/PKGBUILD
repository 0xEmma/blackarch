# Archtrack maintainer: Evan Teitelman <teitelmanevan at gmail dot com>
groups=(archtrack archtrack-password-attacks)

pkgname='hydra'
pkgver=7.4.2
pkgrel=2
pkgdesc='A very fast network logon cracker which support many different services.'
arch=('i686' 'x86_64')
url="http://www.thc.org/thc-hydra/"
license="GPL3"
depends=('postgresql-libs' 'subversion' 'libssh' 'flamerobin' 'libmysqlclient' 'libidn')
source=("http://www.thc.org/releases/${pkgname}-${pkgver}.tar.gz"
        "fix-locations.patch")
md5sums=('c38d9daff27ff1a46e7a31f4550fe714'
         '869ca964fac894a670806d27a51ec843')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 < ../fix-locations.patch
  ./configure
  make
  return 0
}

package() {
  cd $srcdir/$pkgname-$pkgver
  # Needed or else make install will not
  # install sample default password files
  mkdir -p $pkgdir/usr/etc/
  make PREFIX=$pkgdir/usr install
  # Move things installed in the wrong places to the right places
  mkdir -p $pkgdir/usr/share $pkgdir/etc
  mv $pkgdir/usr/man/ $pkgdir/usr/share/
  mv $pkgdir/usr/etc/ $pkgdir/etc/hydra/
}
