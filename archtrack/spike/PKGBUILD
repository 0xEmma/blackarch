pkgname='spike'
pkgver=2.9
pkgrel=2
pkgdesc="IMMUNITYsec's fuzzer creation kit in C"
groups=(blackarch blackarch-analysis)
arch=('i686' 'x86_64')
url="http://www.immunitysec.com/resources-freesoftware.shtml"
license=('GPL2')
depends=('gcc' 'automake' 'openssl')
optdepends=('python2: needed to run SPIKE_Console.py python GUI'
            'wxpython: needed to run SPIKE_Console.py python GUI')
source=("http://www.immunitysec.com/downloads/SPIKE${pkgver}.tgz")
md5sums=(5e41c40904db2b3da06f181e8815aedd)
sha1sums=(efc403a5f48f7172402f8896f398d2b1b9881f29)

prepare() {
  cd $srcdir/SPIKE/SPIKE/src
  sed -i 's|/usr/bin/python|/usr/bin/python2|g' SPIKE_Console.py
}

build() {
  cd $srcdir/SPIKE/SPIKE/src
  automake
  autoheader
  autoconf
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/SPIKE/SPIKE/src

  install -dm755 $pkgdir/usr/lib
  install -dm755 $pkgdir/usr/share/$pkgname
  install -dm755 $pkgdir/usr/share/doc/$pkgname
  install -m755 libdlrpc.so "$pkgdir/usr/lib/"

  cp --no-preserve=ownership -r * $pkgdir/usr/share/$pkgname

  cd $srcdir/SPIKE/SPIKE
  install -m644 README* TODO* CHANGELOG* *.txt \
          $pkgdir/usr/share/doc/$pkgname
  cp --no-preserve=ownership -r data dcedump encrypted include $pkgdir/usr/share/$pkgname/
  install -m644 documentation $pkgdir/usr/share/doc/$pkgname

  cd $srcdir/SPIKE
  cp --no-preserve=ownership -r backups $pkgdir/usr/share/$pkgname/
  install -Dm644 AUTHOR $pkgdir/usr/share/doc/$pkgname/AUTHOR
  install -Dm644 dave_immunitysec_com.asc $pkgdir/usr/share/doc/$pkgname/dave_immunitysec_com.asc
}
