# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
# AUR Maintainer: jsteel <mail at jsteel dot org>
# Contributor: Paul Burton <paulburton89 at gmail dot com>

pkgname=fbcmd
pkgver=1.1
pkgrel=4
pkgdesc="A Command Line Interface (CLI) for Facebook"
arch=('any')
url="http://fbcmd.dtompkins.com"
license=('GPL3')
depends=('php')
source=("http://fbcmd.dtompkins.com/downloads/fbcmd-${pkgver//./-}.zip"
        'php.ini')
md5sums=('b2172219e66db88905a84737804ea206'
         'a20482d3163727dc5ac8052d2c00bdcc')

package() {
  cd "$srcdir/fbcmd"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/fbcmd"
  mkdir -p "$pkgdir/usr/share/doc/fbcmd"

  # Share
  install -m755 fbcmd.php "$pkgdir/usr/share/fbcmd"
  install -m644 ../php.ini "$pkgdir/usr/share/fbcmd"
  cp -r fbcmd_update.php facebook support "$pkgdir/usr/share/fbcmd"

  # Doc
  cp filelist.txt readme.txt "$pkgdir/usr/share/doc/fbcmd"

  cat >> "$pkgdir/usr/bin/fbcmd" <<EOF
#!/bin/sh
exec php -c /usr/share/fbcmd/php.ini /usr/share/fbcmd/fbcmd.php "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/fbcmd"
}
