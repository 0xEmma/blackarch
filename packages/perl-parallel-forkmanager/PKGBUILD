# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='perl-parallel-forkmanager'
_lastauthor='S/SZ/SZABGAB'
_pkgname='Parallel-ForkManager'
pkgver=1.05
pkgrel=1
pkgdesc='A simple parallel processing fork manager.'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
makedepends=('perl-extutils-makemaker')
url="http://search.cpan.org/dist/${_pkgname}/"
source=("http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz")
sha1sums=('3b3c375e04e034cedbc593b93fe789ae47603d5d')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  perl Makefile.PL
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make DESTDIR="$pkgdir" install
}
