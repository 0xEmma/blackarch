# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='mitmproxy'
pkgver='0.11.3'
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-exploitation' 'blackarch-proxy')
pkgdesc='SSL-capable man-in-the-middle HTTP proxy.'
arch=('any')
license=('GPL')
url='http://mitmproxy.org/'
depends=('python2-pyopenssl' 'python2-urwid' 'python2-pyasn1'
         'python2-pillow' 'python2-lxml' 'python2-flask' 'python2-netlib'
         'pathod')
source=("http://mitmproxy.org/download/mitmproxy-${pkgver}.tar.gz")
sha1sums=('e36e7c8d50983d2ae5a4038da278cb65fb4ea2ad')

package() {
  cd "$srcdir/mitmproxy-$pkgver/"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  find "${pkgdir}" -type f -name '*.py' |
  xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'

  rm -rf "${pkgdir}/usr/lib/python2.7/site-packages/"{libpathod,netlib}
}
