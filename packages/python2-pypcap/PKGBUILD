# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=python2-pypcap
pkgver=1.1
pkgrel=6
pkgdesc="Python extension module for libpcap."
url="http://code.google.com/p/pypcap/"
license=('custom')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('python2' 'libpcap')
makedepends=()
source=("http://pypcap.googlecode.com/files/pypcap-$pkgver.tar.gz"
        'python2-pypcap.patch')
sha1sums=('966f62deca16d5086e2ef6694b0c795f273da15c'
          '3909d8ec51bea19c32f8128f8bd6f93813f44a58')

prepare() {
  cd "$srcdir/pypcap-$pkgver"

  patch -p0 < "$srcdir/python2-pypcap.patch"
}

build() {
  cd "$srcdir/pypcap-$pkgver"

  python2 setup.py config
  python2 setup.py build
}

package() {
  cd "$srcdir/pypcap-$pkgver"

  python2 setup.py install "--root=$pkgdir"

  install -Dm644 "$srcdir/pypcap-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/pypcap/LICENSE"
}
