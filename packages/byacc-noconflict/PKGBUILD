# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=byacc-noconflict
pkgver=20130925
pkgrel=1
pkgdesc="Non-conflicting version of byacc not providing bison/yacc"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://invisible-island.net/byacc/"
license=('custom')
depends=('glibc')
source=(ftp://invisible-island.net/byacc/byacc-$pkgver.tgz
        ftp://invisible-island.net/byacc/byacc-$pkgver.tgz.asc)
sha1sums=('6f49b2c730a5ad9882f823f40dc617713f42fc73'
          'SKIP')

build() {
  cd "$srcdir/byacc-$pkgver"

  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/byacc-$pkgver"

  make "DESTDIR=$pkgdir" install

  # License
  sed -n "/is distributed/,/distributed freely/p" README > COPYING
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/byacc/COPYING"

  # Rename
  cd "$pkgdir/usr/bin"
  mv yacc byacc

  cd "$pkgdir/usr/share/man/man1"
  mv yacc.1 byacc.1
}
