# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='lldb'
pkgver='3.5.1'
pkgrel=1
pkgdesc='The LLDB Debugger.'
groups=('blackarch' 'blackarch-debugger')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://llvm.org/'
license=('custom:University of Illinois/NCSA')
depends=('gcc-libs' 'libffi' 'python2' 'gcc>=4.6.2' 'libedit' 'llvm' 'clang')
makedepends=('cmake' 'swig')
provides=('lldb')
conflicts=('lldb')
source=("http://llvm.org/releases/${pkgver}/llvm-${pkgver}.src.tar.xz"
        "http://llvm.org/releases/${pkgver}/cfe-${pkgver}.src.tar.xz"
        "http://llvm.org/releases/${pkgver}/lldb-${pkgver}.src.tar.xz")
sha1sums=('79638cf00584b08fd6eeb1e73ea69b331561e7f6'
          '39d79c0b40cec548a602dcac3adfc594b18149fe'
          '32728e25e6e513528c8c793ae65981150bec7c0d')

prepare() {
  cd "$srcdir"

  mv "cfe-${pkgver}.src" "llvm-${pkgver}.src/tools/clang"
  mv "lldb-${pkgver}.src" "llvm-${pkgver}.src/tools/lldb"

  cd "$srcdir/llvm-${pkgver}.src/tools/lldb"

  sed -i -e "s|python-config|python2-config|" lib/Makefile
  sed -i -e "s|python-config|python2-config|" Makefile
  sed -i -e "s|/usr/bin/env python|&2|" scripts/Python/build-swig-Python.sh
  sed -i -e "s|/usr/bin/env python|&2|" \
    scripts/Python/finish-swig-Python-LLDB.sh
}

build() {
  cd "$srcdir/llvm-${pkgver}.src"

  export CFLAGS="$CFLAGS -fno-tree-pre"
  export CXXFLAGS="$CXXFLAGS -fno-tree-pre"

  mkdir build
  cd build

  _libffi_include="$(pkg-config libffi --cflags-only-I | sed 's/-I//')"

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release \
    -DLLVM_ENABLE_ASSERTIONS=OFF -DLLVM_ENABLE_FFI=ON \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 -DFFI_INCLUDE_PATH=$_libffi_include \
    ..

  make
}

package() {
  cd "$srcdir/llvm-${pkgver}.src"

  local python_dir="$pkgdir/usr/lib/python2.7/site-packages"

  install -Dm644 tools/lldb/LICENSE.TXT \
    "$pkgdir/usr/share/licenses/lldb/LICENSE"

  cd "$srcdir/llvm-${pkgver}.src/build"

  install -Dm755 bin/lldb "$pkgdir/usr/bin/lldb"
  install -Dm755 bin/lldb-gdbserver "$pkgdir/usr/bin/lldb-gdbserver"
  install -Dm755 lib/liblldb.so "$pkgdir/usr/lib/liblldb.so"

  mkdir -p "$python_dir"

  cp -a lib/python2.7/site-packages/lldb "$python_dir"

  ln -sf /usr/lib/liblldb.so "$python_dir/lldb/_lldb.so"
}
