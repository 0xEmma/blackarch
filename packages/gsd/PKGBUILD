# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

# TODO: check license
pkgname=gsd
pkgver=1.2.2
pkgrel=4
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
groups=('blackarch' 'blackarch-scanner')
pkgdesc="OpenVAS GUI frontend"
url='http://www.openvas.org/'
license=('GPL')
depends=('glib2' 'gnutls' 'openvas-libraries' 'qt4' 'qtwebkit' 'icu')
makedepends=('cmake' 'doxygen')
backup=('etc/openvas/gsd_log.conf')
options=('!buildflags' '!makeflags')
source=('http://wald.intevation.org/frs/download.php/1084/gsd-1.2.2.tar.gz'
        'macro-keyword-fix.patch'
        'gsd-enum-fix.patch')
sha1sums=('0a4299d77ef7dc2fb4436b45647c7c5ece85fd75'
          '7164631ff415bc5820a2f40cb76f6022bc20a6fe'
          'ef855602f18732131d2e11d7ccf69c8b4bfea11d')

prepare() {
  cd "$srcdir/gsd-$pkgver"

  patch -p1 < "$srcdir/macro-keyword-fix.patch"
  patch -p1 < "$srcdir/gsd-enum-fix.patch"
}

build() {
  cd "$srcdir/gsd-$pkgver"

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd "$srcdir/gsd-$pkgver"

  make DESTDIR="$pkgdir/" install
}
