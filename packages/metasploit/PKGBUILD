# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='metasploit'
pkgver=24263.15bd92d
pkgrel=1
epoch=3
groups=('blackarch' 'blackarch-exploitation' 'blackarch-fuzzer'
        'blackarch-scanner')
pkgdesc='An open source platform that supports vulnerability research, exploit development and the creation of custom security tools representing the largest collection of quality-assured exploits.'
depends=('ruby1.9' 'ruby1.9-bundler' 'java-environment' 'postgresql' 'libpcap'
         'make')
arch=('any')
optdepends=('dradis: dradis database system')
makedepends=('git')
url='http://www.metasploit.com'
license=('BSD')
options=('!strip')
install='metasploit.install'
source=('git+https://github.com/rapid7/metasploit-framework')
sha1sums=('SKIP')

prepare() {
  cd "$srcdir/metasploit-framework"

  find . -type f -exec sed -i '1s/ruby$/ruby-1.9/g' {} +
}

pkgver() {
  cd "metasploit-framework"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/metasploit-framework"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/etc"
  install -dm755 "$pkgdir/usr/share/metasploit"
  install -dm755 "$pkgdir/usr/share/doc/metasploit"

  # Share.
  cp --no-preserve=ownership -R documentation/* \
    "$pkgdir/usr/share/doc/metasploit"
  cp --no-preserve=ownership README.md "$pkgdir/usr/share/doc/metasploit"

  # Source + Bin.
  cp --no-preserve=ownership -R * "$pkgdir/usr/share/metasploit"
  rm -rf "$pkgdir/usr/share/metasploit"/{README.md,documentation}

  # Conform to the FHS.
  mv "$pkgdir/usr/share/metasploit/config" "$pkgdir/etc/metasploit"
  ln -s /etc/metasploit "$pkgdir/usr/share/metasploit/config"

  # Bin.
  for b in      \
    msfbinscan  \
    msfcli      \
    msfconsole  \
    msfd        \
    msfelfscan  \
    msfencode   \
    msfmachscan \
    msfpayload  \
    msfpescan   \
    msfrop      \
    msfrpc      \
    msfrpcd     \
    msfupdate
  do
  cat > "$pkgdir/usr/bin/$b" <<EOF
#!/bin/sh
exec ruby-1.9 /usr/share/metasploit/$b "\$@"
EOF
done

  chmod +x "$pkgdir"/usr/bin/*
}
