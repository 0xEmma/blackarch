# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='openvas-libraries'
pkgver='7.0.2'
pkgrel=2
pkgdesc="The OpenVAS libraries"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.openvas.org/"
license=('GPL')
depends=('gnutls2' 'libpcap' 'gpgme' 'libssh')
makedepends=('pkg-config' 'cmake' 'bison')
options=('!makeflags !buildflags')
# The file downloaded is determined by the `/1417/` part. Changing `pkgver`
#  does not change the file downloaded so we hard code the version number to
#  avoid confusion.
# Here's a list of downloads for future reference: http://wald.intevation.org/frs/?group_id=29
source=("http://wald.intevation.org/frs/download.php/1671/openvas-libraries-7.0.2.tar.gz")
sha1sums=('3abc9cf8feffe953700b415a727dfd7af734e59a')

prepare() {
  cd "$srcdir/openvas-libraries-$pkgver"

  find . -name 'CMakeLists.txt' -exec sed -i 's/-Werror//' {} \;
}

build() {
  cd "$srcdir/openvas-libraries-$pkgver"

  export PKG_CONFIG_PATH=/usr/gnutls2/lib/pkgconfig/:$PKG_CONFIG_PATH
  CFLAGS="-I/usr/gnutls2/include" LDFLAGS="-L/usr/gnutls2/lib"

  cmake -D CMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_RPATH="/usr/gnutls2/lib/" -DCMAKE_INSTALL_RPATH_USE_LINK_PATH=TRUE -DSBINDIR=/usr/bin \
  -DCMAKE_BUILD_WITH_INSTALL_RPATH=TRUE -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var -DLIBDIR=/usr/lib .
  make
}

package() {
  cd "$srcdir/openvas-libraries-$pkgver"

  make "DESTDIR=$pkgdir/" install
}
