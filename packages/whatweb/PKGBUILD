# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='whatweb'
pkgver='0.4.7'
pkgrel=7
groups=('blackarch' 'blackarch-recon' 'blackarch-webapp')
pkgdesc='Next generation web scanner that identifies what websites are running.'
arch=('any')
url='http://www.morningstarsecurity.com/research/whatweb'
license=('GPL')
depends=('ruby1.8')
source=("http://www.morningstarsecurity.com/downloads/whatweb-${pkgver}.tar.gz")
sha1sums=('2c61ebf52ecac7436cc67d63b5b4e71e067bd87f')

prepare() {
  cd "$srcdir/whatweb-$pkgver"

  sed -i 's|/usr/bin/env ruby|/usr/bin/env ruby-1.8|' whatweb
}

package() {
  cd "$srcdir/whatweb-$pkgver"

  mkdir -p "$pkgdir/usr/share/doc/whatweb"

  install -Dm644 -t "$pkgdir/usr/share/doc/whatweb" CHANGELOG README INSTALL \
    TODO
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/whatweb/LICENSE"

  make DESTDIR=$pkgdir install
}
