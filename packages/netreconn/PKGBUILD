# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='netreconn'
pkgver='1.76'
pkgrel=1
groups=('blackarch' 'blackarch-networking' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='A collection of network scan/recon tools that are relatively small compared to their larger cousins.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://packetstormsecurity.com/files/86076/NetReconn-Scanning-Tool-Collection-1.76.html'
license=('GPL2')
depends=('perl' 'glibc' 'libpcap')
makedepends=('gzip')
source=("http://packetstorm.foofus.com/UNIX/utilities/netreconn-${pkgver}.tar.gz")
sha1sums=('bff5fa1224f1f28133f4114c46860d8f185f83cc')

prepare() {
  cd "$srcdir/netreconn-$pkgver"

  sed -i 's|/usr/local/share/man/man1|/usr/share/man/man1|' Makefile
  sed -i 's|/usr/local/bin|/usr/bin|' Makefile

  for i in *.1
  do
    gzip $i
  done
}

build() {
  cd "$srcdir/netreconn-$pkgver"

  make linux
}

package() {
  cd "$srcdir/netreconn-$pkgver"

  #make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/man/man1/"

  install -m755 -t "$pkgdir/usr/bin/" ndecode nlist nstrobe ntrace ntraf

  for i in *.1.gz
  do
    install -m644 $i "$pkgdir/usr/share/man/man1/$i"
  done

  install -Dm644 README "$pkgdir/usr/share/doc/netreconn/README"
  install -m644 ChangeLog "$pkgdir/usr/share/doc/netreconn/ChangeLog"
  install -m644 TODO "$pkgdir/usr/share/doc/netreconn/TODO"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/netreconn/COPYING"
}
