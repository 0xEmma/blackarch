# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=("python-click" "python2-click")
pkgver='3.3'
pkgrel=1
pkgdesc='Command Line Interface Creation Kit.'
arch=('any')
url='http://click.pocoo.org/'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=("https://github.com/mitsuhiko/click/archive/${pkgver}.tar.gz")
sha1sums=('c6988244cef66666874f291b4439d4ad0ae254ef')

prepare() {
  cd "$srcdir"

	cp -a "$srcdir/click-${pkgver}"{,-python2}
}

build() {
	cd "$srcdir/click-$pkgver"

	python setup.py build

	cd "$srcdir/click-${pkgver}-python2"

	python2 setup.py build
}

package_python-click() {
	depends=("python>=3.3")

  cd "$srcdir/click-$pkgver"

	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}

package_python2-click() {
	depends=("python2>=2.6")

	cd "$srcdir/click-${pkgver}-python2"

	python2 setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
