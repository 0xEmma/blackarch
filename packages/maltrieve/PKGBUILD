# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='maltrieve'
pkgver=60.465f162
pkgrel=1
groups=('blackarch' 'blackarch-malware' 'blackarch-disassembler')
pkgdesc='Originated as a fork of mwcrawler. It retrieves malware directly from the sources as listed at a number of sites.'
arch=('any')
url='https://github.com/technoskald/maltrieve'
license=('GPL3')
depends=('python2' 'python2-beautifulsoup4' 'python2-lxml')
makedepends=('git')
source=('git+https://github.com/technoskald/maltrieve.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/maltrieve"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/maltrieve"

  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/maltrieve"

  install -Dm644 README.md "$pkgdir/usr/share/doc/maltrieve/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/maltrieve/LICENSE"

  cp -a * "$pkgdir/usr/share/maltrieve"

  rm "$pkgdir/usr/share/maltrieve/README.md"
  rm "$pkgdir/usr/share/maltrieve/LICENSE"

  cat > "$pkgdir/usr/bin/maltrieve" <<EOF
#!/bin/sh
cd /usr/share/maltrieve
python2 maltrieve.py "\$@"
EOF

  cat > "$pkgdir/usr/bin/maltrievecategorizer" <<EOF
#!/bin/sh
cd /usr/share/maltrieve
sh maltrievecategorizer.sh "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/maltrieve"
  chmod +x "$pkgdir/usr/bin/maltrievecategorizer"
}
