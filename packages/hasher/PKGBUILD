# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='hasher'
pkgver=48.40173c5
pkgrel=1
groups=('blackarch' 'blackarch-cracker' 'blackarch-crypto')
pkgdesc='A tool that allows you to quickly hash plaintext strings, or compare hashed values with a plaintext locally.'
arch=('any')
url='https://github.com/ChrisTruncer/Hasher'
depends=('python2' 'python2-passlib' 'python2-py-bcrypt')
makedepends=('git')
license=('GPL3')
source=('git+https://github.com/ChrisTruncer/Hasher.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Hasher"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/Hasher"

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' Hasher.py
}

package() {
  cd "$srcdir/Hasher"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/hasher"

  cp -a Hasher.py hash_ops common "$pkgdir/usr/share/hasher/"
  install -Dm644 -t "$pkgdir/usr/share/doc/hasher/" README.md CHANGELOG
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/hasher/LICENSE"

  cat > "$pkgdir/usr/bin/hasher" << EOF
#!/bin/sh
cd /usr/share/hasher
exec python2 Hasher.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/hasher"
}
