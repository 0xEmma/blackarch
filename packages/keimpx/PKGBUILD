# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='keimpx'
pkgver=159.ebe5cdc
pkgrel=1
groups=('blackarch' 'blackarch-cracker')
pkgdesc='Tool to verify the usefulness of credentials across a network over SMB.'
url='https://github.com/inquisb/keimpx'
license=('Apache')
depends=('python2' 'impacket' 'python2-crypto')
makedepends=('unzip')
arch=('any')
source=('git+https://github.com/inquisb/keimpx.git'
        'setup.py-remove_py2exe.patch')
sha1sums=('SKIP'
          'd54425f9d34d17870d13eade888207252233cfb5')

pkgver() {
  cd "$srcdir/keimpx"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/keimpx"

  sed -i 's/\/usr\/bin\/env python/\/usr\/bin\/env python2/' keimpx.py
  sed -i 's/keimpx_path =/keimpx_path = \"\/usr\/share\/keimpx\" #/' keimpx.py

  patch setup.py < "$srcdir/setup.py-remove_py2exe.patch"
}

package() {
  cd "$srcdir/keimpx"

  install -dm755 "$pkgdir/usr/share/keimpx"

  python2 setup.py install --prefix="$pkgdir/usr"

  install -Dm755 keimpx.py "$pkgdir/usr/bin/keimpx"

  mv "$pkgdir/usr/contrib" "$pkgdir/usr/share/keimpx"
}
