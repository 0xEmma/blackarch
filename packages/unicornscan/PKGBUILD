pkgname=unicornscan
pkgver=0.4.7
pkgrel=3
url='http://www.unicornscan.org/'
pkgdesc="A new information gathering and correlation engine."
arch=('i686' 'x86_64')
license=('GPL')
depends=('libpcap' 'postgresql-libs' 'geoip' 'libdnet' 'libltdl')
makedepends=('flex' 'bison')
source=("http://www.unicornscan.org/releases/unicornscan-$pkgver-2.tar.bz2")
md5sums=('4c5f272eb38c333c0094c32317edf758')

build() {
  cd "$srcdir/unicornscan-$pkgver"

  # Prevent crashing geoip dependency.
  export LDFLAGS="${LDFLAGS/--as-needed,/}"
  ./configure CFLAGS="$CFLAGS -D_GNU_SOURCE" --prefix=/usr --sysconfdir=/etc --localstatedir=/var --with-pgsql --with-geoip
  make
}

package() {
  cd "$srcdir/unicornscan-$pkgver"

  make "DESTDIR=$pkgdir" install
  ln -s /usr/share/GeoIP/GeoIP.dat "$pkgdir/etc/unicornscan/GeoIP.dat"
}
