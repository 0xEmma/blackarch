# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='dnsbrute'
pkgver='2.b1dc84a'
pkgrel=1
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='Multi theaded DNS bruteforcing.'
arch=('any')
url='https://github.com/d4rkcat/dnsbrute'
license=('GPL2')
depends=('python2' 'python2-dnspython')
makedepends=('git')
source=('git+https://github.com/d4rkcat/dnsbrute.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/dnsbrute"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/dnsbrute"

  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/dnsbrute"

  install -Dm644 README.md "$pkgdir/usr/share/doc/dnsbrute/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/dnsbrute/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/dnsbrute/"

  cat > "$pkgdir/usr/bin/dnsbrute" << EOF
#!/bin/sh
cd /usr/share/dnsbrute
exec python2 dnsbrute.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/dnsbrute"
}
