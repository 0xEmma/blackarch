# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='peda'
pkgver=49.9b1d28d
pkgrel=2
groups=('blackarch' 'blackarch-debugger' 'blackarch-exploitation')
pkgdesc='Python Exploit Development Assistance for GDB.'
arch=('any')
url='https://github.com/longld/peda'
license=('GPL3')
depends=('python2' 'python2-httplib2' 'gdb' 'nasm' 'binutils')
makedepends=('git')
source=('git+https://github.com/longld/peda.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/peda"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/peda"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/peda"

  install -Dm644 README "$pkgdir/usr/share/peda/README"
  install -Dm644 README.md "$pkgdir/usr/share/peda/README"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/peda/LICENSE"

  rm README README.md LICENSE

  cp --no-preserve=ownership -a * "$pkgdir/usr/share/peda"

  cat > "$pkgdir/usr/bin/peda" << EOF
#!/bin/sh
cd /usr/share/peda
exec python2 peda.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/peda"
}
