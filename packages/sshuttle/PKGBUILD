# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='sshuttle'
pkgver=198.9ce2fa0
pkgrel=2
pkgdesc="Transparent proxy server that works as a poor man's VPN. Forwards all TCP packets over ssh (and even DNS requests when using --dns option). Doesn't require admin privileges on the server side."
groups=('blackarch' 'blackarch-proxy' 'blackarch-networking')
arch=('any')
url='https://github.com/apenwarr/sshuttle'
license=('GPL')
depends=('python2' 'iptables')
makedepends=('git')
source=('git+https://github.com/apenwarr/sshuttle.git'
        'arch-install.patch')
sha1sums=('SKIP'
          'd6c4f163d43e1bf987e0d025692a7a0c6b2b418c')

pkgver() {
  cd "$srcdir/sshuttle"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/sshuttle"

  patch -p1 -i "$srcdir/arch-install.patch"

  cd "Documentation"

  sed -i 's/python/python2/' md2man.py
}

build() {
  cd "$srcdir/sshuttle"

  make
}

package() {
  cd "$srcdir/sshuttle"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/sshuttle"

  install -Dm644 README.md "$pkgdir/usr/share/doc/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/sshuttle/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/sshuttle"

  install -Dm755 "$srcdir/sshuttle/sshuttle" "$pkgdir/usr/bin/sshuttle"
}
