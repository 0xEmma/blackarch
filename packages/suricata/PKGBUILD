# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=suricata
pkgver=1.4.6
pkgrel=2
pkgdesc="An Open Source Next Generation Intrusion Detection and Prevention Engine"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://openinfosecfoundation.org/index.php/download-suricata"
license=('GPL2')
depends=('libcap-ng' 'libnet' 'libpcap' 'libyaml' 'pcre')
optdepends=('snort: suricata can use rulesets provided by snort')
backup=('etc/suricata/suricata.yaml')
source=(http://openinfosecfoundation.org/download/suricata-$pkgver.tar.gz)
sha256sums=('5cf5d76dd63d06b993912301edc8afa06f886d0b68740239cc7df49b00800f8e')

build() {
  cd ${srcdir}/suricata-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc/suricata
  make
}

package() {
  cd ${srcdir}/suricata-$pkgver

  make DESTDIR=${pkgdir} install

  install -d ${pkgdir}/var/log/suricata

  install -D -m644 suricata.yaml "${pkgdir}/etc/suricata/suricata.yaml"
  install -D -m644 classification.config "${pkgdir}/etc/suricata/classification.config"
  install -D -m644 reference.config "${pkgdir}/etc/suricata/reference.config"

  install -d "${pkgdir}/etc/suricata/rules"
  install -D -m644 rules/*.rules "${pkgdir}/etc/suricata/rules/"
}
