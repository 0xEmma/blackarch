# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='suricata'
pkgver='2.0.1'
pkgrel=2
pkgdesc='An Open Source Next Generation Intrusion Detection and Prevention Engine.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://openinfosecfoundation.org/index.php/download-suricata"
groups=('blackarch' 'blackarch-defensive')
license=('GPL2')
depends=('libcap-ng' 'libnet' 'libpcap' 'libyaml' 'pcre')
optdepends=('snort: suricata can use rulesets provided by snort')
backup=('etc/suricata/suricata.yaml')
source=("http://openinfosecfoundation.org/download/suricata-${pkgver}.tar.gz")
sha1sums=('e18a1108244985907db244434e52ae9e2a3d1eb2')

build() {
  cd "$srcdir/suricata-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc/suricata

  make
}

package() {
  cd "$srcdir/suricata-$pkgver"

  mkdir -p "$pkgdir/var/log/suricata"

  make DESTDIR="$pkgdir" install

  install -Dm644 suricata.yaml "$pkgdir/etc/suricata/suricata.yaml"
  install -Dm644 classification.config \
    "$pkgdir/etc/suricata/classification.config"
  install -Dm644 reference.config "$pkgdir/etc/suricata/reference.config"

  install -d "$pkgdir/etc/suricata/rules"
  install -Dm644 rules/*.rules "$pkgdir/etc/suricata/rules/"
}
