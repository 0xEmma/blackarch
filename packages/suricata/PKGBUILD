# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='suricata'
pkgver='2.0.3'
pkgrel=1
pkgdesc='An Open Source Next Generation Intrusion Detection and Prevention Engine.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://openinfosecfoundation.org/index.php/download-suricata"
groups=('blackarch' 'blackarch-defensive')
license=('GPL2')
depends=('zlib' 'nss' 'jansson' 'libcap-ng' 'libnetfilter_queue' 'libnfnetlink'
         'libnet' 'libpcap' 'libyaml' 'pcre' 'python2-magic' 'file')
optdepends=('snort: suricata can use rulesets provided by snort')
backup=('etc/suricata/suricata.yaml')
source=("http://openinfosecfoundation.org/download/suricata-${pkgver}.tar.gz")
sha1sums=('6fb6e486e58211a0dd958dc54d2b58e843c02a29')

build() {
  cd "$srcdir/suricata-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc/suricata

  make
}

package() {
  cd "$srcdir/suricata-$pkgver"

  mkdir -p "$pkgdir/var/log/suricata"

  make DESTDIR="$pkgdir" install

  install -Dm644 suricata.yaml "$pkgdir/etc/suricata/suricata.yaml"
  install -Dm644 classification.config \
    "$pkgdir/etc/suricata/classification.config"
  install -Dm644 reference.config "$pkgdir/etc/suricata/reference.config"

  mkdir -p "$pkgdir/etc/suricata/rules"
  install -Dm644 rules/*.rules "$pkgdir/etc/suricata/rules/"
}
