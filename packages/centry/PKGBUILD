# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='centry'
pkgver=68.4cdf8c5
pkgrel=1
groups=('blackarch' 'blackarch-misc' 'blackarch-defensive')
pkgdesc='Cold boot & DMA protection'
url='https://github.com/0xPoly/Centry'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL3')
depends=('python' 'python-pmw')
source=('git+https://github.com/0xPoly/Centry'
        'centry.patch')
sha1sums=('SKIP'
          '9ce565ade8f1596a1123068a7c87143947f8ef08')

pkgver() {
  cd "$srcdir/Centry"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/Centry"

  patch < "$srcdir/centry.patch"
}

package() {
  cd "$srcdir/Centry"

  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share"

  cp -aR $srcdir/Centry "$pkgdir/usr/share/centry"

  cat > "$pkgdir/usr/bin/centry" << EOF
#!/bin/bash
cd /usr/share/centry
exec python centry.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/centry"
}
