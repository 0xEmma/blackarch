# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='driftnet'
pkgver='0.1.6'
pkgrel=11
groups=('blackarch' 'blackarch-scanner' 'blackarch-sniffer')
pkgdesc='Listens to network traffic and picks out images from TCP streams it observes.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.ex-parrot.com/~chris/driftnet/'
license=('GPL')
depends=('libjpeg' 'libpcap' 'gtk2' 'giflib')
makedepends=('imake')
source=("http://www.ex-parrot.com/~chris/driftnet/driftnet-${pkgver}.tar.gz"
        'gif.patch'
        'driftnet.patch')
sha1sums=('279cb3a596f354e7e9c6ac3cc456b3d11dced2ff'
          'b1fd0acaca9ec289fba81b62129bbc3e8c114ccf'
          'd09422f728639052e4a69d2a86092195276bddf9')

prepare() {
  cd "$srcdir/driftnet-$pkgver"

  # This library has been combined with giflib.
  sed -i 's/-lungif//' Makefile

  patch < "$srcdir/gif.patch"
  patch < "$srcdir/driftnet.patch"
}

build() {
  cd "$srcdir/driftnet-$pkgver"

  make 
}

package() {
  cd "$srcdir/driftnet-$pkgver"

  install -Dm755 driftnet "$pkgdir/usr/bin/driftnet"
}
