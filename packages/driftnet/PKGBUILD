# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='driftnet'
pkgver='0.1.6'
pkgrel=13
groups=('blackarch' 'blackarch-scanner' 'blackarch-sniffer')
pkgdesc='Listens to network traffic and picks out images from TCP streams it observes.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.ex-parrot.com/~chris/driftnet/'
license=('GPL')
depends=('libjpeg' 'libpcap' 'gtk2' 'giflib')
makedepends=('imake')
source=("http://www.ex-parrot.com/~chris/driftnet/driftnet-${pkgver}.tar.gz"
        'gif.patch'
        'driftnet.patch'
        'interface.patch')
sha1sums=('279cb3a596f354e7e9c6ac3cc456b3d11dced2ff'
          '3174a58ef3af882787eee251c51cb5bb890d316d'
          'cecbac26761b94b4462c6dd08e070a917d0a1e7a'
          '6d46c7c52c442cc12652ab5489ccd4cd483f3462')

prepare() {
  cd "$srcdir/driftnet-$pkgver"

  # This library has been combined with giflib.
  sed -i 's/-lungif//' Makefile

  patch < "$srcdir/gif.patch"
  patch < "$srcdir/driftnet.patch"
  patch < "$srcdir/interface.patch"
}

build() {
  cd "$srcdir/driftnet-$pkgver"

  make 
}

package() {
  cd "$srcdir/driftnet-$pkgver"

  install -Dm755 driftnet "$pkgdir/usr/bin/driftnet"
}
