# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='maskprocessor'
pkgver='0.69'
pkgrel=3
groups=('blackarch' 'blackarch-cracker')
pkgdesc="A High-Performance word generator with a per-position configurable charset"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://hashcat.net/wiki/doku.php?id=maskprocessor'
license=('custom')
makedepends=('p7zip')
source=("http://hashcat.net/files/maskprocessor-$pkgver.7z")
sha1sums=('e7092becda9cb8b3d1d2c823b4d3f3e898b1f8b5')

prepare() {
  cd "$srcdir/maskprocessor-$pkgver"

  sed -i 's|dicts/|"$1"/|' generate.sh
  sed -i 's|./mp64.bin|/usr/bin/maskprocessor|' generate.sh
}

package() {
  cd "$srcdir/maskprocessor-$pkgver"

  # Base directories.
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/maskprocessor"

  # Bin.
  if [[ "$CARCH" == 'x86_64' ]]; then
    install -m755 mp64.bin "$pkgdir/usr/bin/mp64"
    ln -s mp64 "$pkgdir/usr/bin/maskprocessor"
  elif [[ "$CARCH" == 'i686' ]]; then
    install -m755 mp32.bin "$pkgdir/usr/bin/mp32"
    ln -s mp32 "$pkgdir/usr/bin/maskprocessor"
  fi
  install -m755 generate.sh "$pkgdir/usr/share/maskprocessor"
}
