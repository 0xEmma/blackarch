# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='hydra'
pkgver='7.6'
pkgrel=1
groups=('blackarch' 'blackarch-cracker')
pkgdesc="A very fast network logon cracker which support many different services."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.thc.org/thc-hydra/'
license=('GPL3')
depends=('postgresql-libs' 'subversion' 'libssh' 'flamerobin' 'libmariadbclient' 'libidn')
source=("http://www.thc.org/releases/hydra-$pkgver.tar.gz")
sha1sums=('7df230bb4f228a5cdc776ef1a215a1523b58f40b')

build() {
  cd $srcdir/hydra-${pkgver}

  ./configure
  make
}

package() {
  cd $srcdir/hydra-${pkgver}

  # Needed or else make install will not
  # install sample default password files.
  install -dm755 $pkgdir/usr/etc
  make PREFIX=$pkgdir/usr install

  # Conform to the FHS.
  install -dm755 "$pkgdir/usr/share" "$pkgdir/etc"
  mv "$pkgdir/usr/man" "$pkgdir/usr/share"
  mv "$pkgdir/usr/etc" "$pkgdir/etc/hydra"
}
