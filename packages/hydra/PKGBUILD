# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='hydra'
pkgver='8.0'
pkgrel=1
groups=('blackarch' 'blackarch-cracker')
pkgdesc='A very fast network logon cracker which support many different services.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.thc.org/thc-hydra/'
license=('GPL3')
depends=('postgresql-libs' 'subversion' 'libssh' 'libmariadbclient'
         'libidn')
source=("http://www.thc.org/releases/hydra-${pkgver}.tar.gz")
sha1sums=('d1a705985846caf77c291461f391a43457cc76e5')

build() {
  cd "$srcdir/hydra-${pkgver}"

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/hydra-${pkgver}"

  # Needed or else make install will not
  # install sample default password files.
  install -dm755 "$pkgdir/usr/etc"
  make PREFIX="$pkgdir/usr" install

  # Conform to the FHS.
  install -dm755 "$pkgdir/usr/share" "$pkgdir/etc"

  mv "$pkgdir/usr/man" "$pkgdir/usr/share"
  mv "$pkgdir/usr/etc" "$pkgdir/etc/hydra"
}
