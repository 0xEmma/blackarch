# AUR Maintainer: Max Roder <maxroder@web.de>

# To port this PKGBUILD to another language of tor-browser you
# have to change tor-browser-en, $_language, $pkgdesc and $url in PKGBUILD
# AND (!) the first line in the .install file!

pkgname='tor-browser-en'
pkgver='3.6.2'
_realpkgver='3.6.2'
_realpkgver_i686='3.6.2'
_language='en-US'
pkgrel='1'
pkgdesc='Tor Browser Bundle: Anonymous browsing using firefox and tor'
url='https://www.torproject.org/projects/torbrowser.html.en'
arch=('x86_64' 'i686')
license=('GPL')
depends=()
optdepends=('zenity: simple dialog boxes'
      'kdebase-kdialog: KDE dialog boxes'
      'libnotify: Gnome dialog boxes')
install="tor-browser-en.install"

# Workaround due to different versions depending on CARCH
if [[ "$CARCH" == 'i686' ]]; then
  _realpkgver=${_realpkgver_i686}
  _pkgarch='32'
else
  _pkgarch='64'
fi

source=("https://www.torproject.org/dist/torbrowser/${pkgver}/tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz"
        "https://www.torproject.org/dist/torbrowser/${pkgver}/tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz.asc"
        "tor-browser-en.desktop"
        "tor-browser-en.png"
        "tor-browser-en.sh")

if [[ "$CARCH" == 'x86_64' ]]; then
   sha256sums=('953546cfec1d539908148feaca7142d9f0f9cc18c12c7a6b92197ac81d8ca56d'
               '8e6f800238dec9aa08c28875ea05ba01ced7d538857673136ea5617a25ffb438')
else
   sha256sums=('be96534e6ab07395fb4d9b28c6f8e3d482a28cdf5a8db5801941c3e834ff42e4'
               'bdd7ed0531bdd380f027445635065759992a6bdd19826945e2d33f5f27d84521')
fi
sha256sums+=('3d5fc01f2cfbae0a00b7117b0b0a24028d1686e6f81a347809f74de2d8522ff7'
      '17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
      'f25fedfa77b19a96488529e6d387411fb49a82e99e3a1a9708b88ad8b6e7d2aa')

noextract=("tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz")

package() {
  cd "${srcdir}"

  sed -i "s/REPL_NAME/tor-browser-en/g"  tor-browser-en.sh
  sed -i "s/REPL_VERSION/${_realpkgver}/g" tor-browser-en.sh
  sed -i "s/REPL_LANGUAGE/${_language}/g" tor-browser-en.sh

  sed -i "s/REPL_NAME/tor-browser-en/g"  tor-browser-en.desktop
  sed -i "s/REPL_LANGUAGE/${_language}/g" tor-browser-en.desktop
  sed -i "s/REPL_COMMENT/${pkgdesc}/g" tor-browser-en.desktop

  install -Dm 644 tor-browser-en.desktop $pkgdir/usr/share/applications/tor-browser-en.desktop
  install -Dm 644 tor-browser-en.png $pkgdir/usr/share/pixmaps/tor-browser-en.png
  install -Dm 755 tor-browser-en.sh $pkgdir/usr/bin/tor-browser-en

  install -Dm 644 tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz $pkgdir/opt/tor-browser-en/tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz
}

