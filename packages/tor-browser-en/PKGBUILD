# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='tor-browser-en'
pkgver='4.0.1'
_realpkgver='4.0.1'
_realpkgver_i686='4.0.1'
_language='en-US'
pkgrel=1
pkgdesc='Tor Browser Bundle: Anonymous browsing using firefox and tor'
url='https://www.torproject.org/projects/torbrowser.html.en'
groups=('blackarch')
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types'
         'dbus-glib' 'alsa-lib' 'desktop-file-utils' 'hicolor-icon-theme'
         'libvpx' 'icu' 'libevent' 'nss' 'hunspell' 'sqlite')
optdepends=('zenity: simple dialog boxes'
            'kdebase-kdialog: KDE dialog boxes'
            'gst-plugins-good: h.264 video'
            'gst-libav: h.264 video'
            'libpulse: PulseAudio audio driver'
            'libnotify: Gnome dialog boxes')
install='tor-browser-en.install'

# Workaround due to different versions depending on CARCH
if [[ "$CARCH" == 'i686' ]]; then
	_realpkgver=${_realpkgver_i686}
	_pkgarch='32'
else
	_pkgarch='64'
fi

source=("https://www.torproject.org/dist/torbrowser/${pkgver}/tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz"
		"https://www.torproject.org/dist/torbrowser/${pkgver}/tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz.asc"
		"tor-browser-en.desktop"
		"tor-browser-en.png"
		"tor-browser-en.sh")
if [[ "$CARCH" == 'x86_64' ]]; then
sha256sums=('53f97d10d5cd17695e84a5eba52fa099f6151e21d409a58feee83327cf33e3b3'
            'SKIP'
            '3d5fc01f2cfbae0a00b7117b0b0a24028d1686e6f81a347809f74de2d8522ff7'
            '17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
            '1c1fbfa21973a2991db8a58f718656fc835dda03257c951432500bbd5dd8d3f5')
else
sha256sums+=('3d5fc01f2cfbae0a00b7117b0b0a24028d1686e6f81a347809f74de2d8522ff7'
               '17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
               '1c1fbfa21973a2991db8a58f718656fc835dda03257c951432500bbd5dd8d3f5')
fi
noextract=("tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz")

package() {
	cd "${srcdir}"

	sed -i "s/REPL_NAME/tor-browser-en/g" tor-browser-en.sh
	sed -i "s/REPL_VERSION/${_realpkgver}/g" tor-browser-en.sh
	sed -i "s/REPL_LANGUAGE/${_language}/g"		tor-browser-en.sh

	sed -i "s/REPL_NAME/tor-browser-en/g"			tor-browser-en.desktop
	sed -i "s/REPL_LANGUAGE/${_language}/g"		tor-browser-en.desktop
	sed -i "s/REPL_COMMENT/${pkgdesc}/g"		tor-browser-en.desktop

	install -Dm 644 tor-browser-en.desktop	${pkgdir}/usr/share/applications/tor-browser-en.desktop
	install -Dm 644 tor-browser-en.png		${pkgdir}/usr/share/pixmaps/tor-browser-en.png
	install -Dm 755 tor-browser-en.sh		${pkgdir}/usr/bin/tor-browser-en

	install -Dm 644 tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz ${pkgdir}/opt/tor-browser-en/tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz
}
