# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=dex2jar
pkgver=0.0.9.13
_dlsuffix=
pkgrel=5
groups=('blackarch' 'blackarch-hardware' 'blackarch-reversing')
pkgdesc="A tool for converting Android's .dex format to Java's .class format"
arch=('any')
url='http://code.google.com/p/dex2jar'
license=('Apache')
depends=('java-environment')
source=("http://dex2jar.googlecode.com/files/dex2jar-$pkgver${_dlsuffix}.zip")
sha1sums=('89057a41d646b15a1a9bb38747371b35d32b326b')

package() {
  cd "$srcdir/dex2jar-$pkgver"

  # Make base directories.
  install -dm755 "$pkgdir/usr/bin"
  # This package provides all of its own libraries.
  install -dm755 "$pkgdir/opt/dex2jar/lib"

  install -m644 lib/*.jar "$pkgdir/opt/dex2jar/lib"
  install -m755 *.sh "$pkgdir/opt/dex2jar"

  for bin in d2j-*.sh ; do
    ln -s /opt/dex2jar/$bin "$pkgdir/usr/bin/${bin%.*}"
  done
}
