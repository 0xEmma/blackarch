# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='openvas-cli'
pkgver='1.3.0'
pkgrel=1
groups=('blackarch' 'blackarch-scanner')
pkgdesc="The OpenVAS Command-Line Interface"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.openvas.org/"
license=('GPL')
depends=('openvas-libraries' 'xmltoman' 'gnutls2' 'glib2')
makedepends=('pkg-config' 'cmake')
options=('!makeflags' '!buildflags')
# The file downloaded is determined by the `/1323/` part. Changing `pkgver`
#  does not change the file downloaded so we hard code the version number to
#  avoid confusion.
source=('http://wald.intevation.org/frs/download.php/1633/openvas-cli-1.3.0.tar.gz')
sha1sums=('f96ec790eb6290b1dfccf4c18ef6836d26702325')

build() {
  cd "$srcdir/openvas-cli-$pkgver"

  export PKG_CONFIG_PATH=/usr/gnutls2/lib/pkgconfig/:$PKG_CONFIG_PATH
  CFLAGS="-I/usr/gnutls2/include" LDFLAGS="-L/usr/gnutls2/lib"

  cmake -D CMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_RPATH="/usr/gnutls2/lib/" -DCMAKE_INSTALL_RPATH_USE_LINK_PATH=TRUE -DSBINDIR=/usr/bin \
  -DCMAKE_BUILD_WITH_INSTALL_RPATH=TRUE -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var -DLIBDIR=/usr/lib .
  make
}

package() {
  cd "$srcdir/openvas-cli-$pkgver"

  make DESTDIR="$pkgdir/" install
}
