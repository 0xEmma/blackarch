# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='osm-gps-map'
pkgver='0.7.3'
pkgrel=5
pkgdesc='GTK+ library for showing OSM tiles'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://nzjrs.github.com/osm-gps-map/'
license=('GPL')
depends=('gtk2' 'libsoup')
source=("http://www.johnstowers.co.nz/files/osm-gps-map/osm-gps-map-$pkgver.tar.gz")
sha1sums=('fae4357ffa18e511d5a642c2e65f5316a123ba93')

build() {
  cd "$srcdir/osm-gps-map-$pkgver"

  ./configure --prefix=/usr --enable-introspection=no \
    --htmldir=/usr/share/osm-gps-map --dvidir=/usr/share/doc/osm-gps-map \
    --pdfdir=/usr/share/doc/osm-gps-map --psdir=/usr/share/doc/osm-gps-map

  make
}

package() {
  cd "$srcdir/osm-gps-map-$pkgver"

  install -dm755 "$pkgdir/usr/share/doc/osm-gps-map"
  install -dm755 "$pkgdir/usr/share/licenses/osm-gps-map"

  install -Dm644 AUTHORS "$pkgdir/usr/share/doc/osm-gps-map/AUTHORS"
  install -Dm644 ChangeLog "$pkgdir/usr/share/doc/osm-gps-map/ChangeLog"
  install -Dm644 INSTALL "$pkgdir/usr/share/doc/osm-gps-map/INSTALL"
  install -Dm644 NEWS "$pkgdir/usr/share/doc/osm-gps-map/NEWS"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/osm-gps-map/COPYING"

  make DESTDIR=$pkgdir install

  rm -rf "$pkgdir/usr/doc"
}
