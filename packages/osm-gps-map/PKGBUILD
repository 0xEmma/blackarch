# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='osm-gps-map'
pkgver='1.1.0'
pkgrel=1
pkgdesc='GTK+ library for showing OSM tiles'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://nzjrs.github.com/osm-gps-map/'
license=('GPL')
depends=('gtk2' 'libsoup' 'gtk3')
source=("https://github.com/nzjrs/osm-gps-map/releases/download/$pkgver/osm-gps-map-${pkgver}.tar.gz")
sha1sums=('070db6bb55b5b085793aefbd263ac940e9b2400e')

build() {
  cd "$srcdir/osm-gps-map-$pkgver"

  ./configure --prefix=/usr --enable-introspection=no \
    --htmldir=/usr/share/osm-gps-map --dvidir=/usr/share/doc/osm-gps-map \
    --pdfdir=/usr/share/doc/osm-gps-map --psdir=/usr/share/doc/osm-gps-map

  make
}

package() {
  cd "$srcdir/osm-gps-map-$pkgver"

  mkdir -p "$pkgdir/usr/share/doc/osm-gps-map"
  mkdir -p "$pkgdir/usr/share/licenses/osm-gps-map"

  install -Dm644 -t "$pkgdir/usr/share/doc/osm-gps-map" AUTHORS ChangeLog NEWS
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/osm-gps-map/COPYING"

  make DESTDIR=$pkgdir install

  rm -rf "$pkgdir/usr/doc"
}
