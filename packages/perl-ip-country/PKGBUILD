# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='perl-ip-country'
pkgver='2.27'
pkgrel=1
pkgdesc='Fast lookup of country codes from IP addresses.'
arch=('any')
url='http://search.cpan.org/~NWETTERS/IP-Country'
license=('GPL' 'PerlArtistic')
depends=('perl-geography-countries')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/N/NW/NWETTERS/IP-Country-${pkgver}.tar.gz")
sha1sums=('889644a7c49714fa6968a0d78f5dfc091f4b0181')

prepare() {
  cd "$srcdir/IP-Country-$pkgver"

  sed -i '/^auto_install/d' Makefile.PL
}

build() {
  cd "$srcdir/IP-Country-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd "$srcdir/IP-Country-$pkgver"

  make install DESTDIR=$pkgdir

  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
}
