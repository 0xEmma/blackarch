# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='dirbuster'
pkgver='1.0_RC1'
pkgrel=7
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
pkgdesc="An application designed to brute force directories and files names on web/application servers"
url='http://www.owasp.org/index.php/Category:OWASP_DirBuster_Project'
license=('LGPL')
arch=('any')
depends=('java-environment')
source=("http://downloads.sourceforge.net/dirbuster/DirBuster-${pkgver/_/-}.tar.bz2")
sha1sums=('aa0807e42ee9794c31398e77cb473d00eaa33f56')

package() {
  cd "$srcdir"

  # Base directories.
  install -dm755 "$pkgdir/opt"
  install -dm755 "$pkgdir/usr/bin"

  cp --no-preserve=ownership -r DirBuster-${pkgver/_/-} "$pkgdir/opt/dirbuster"

  cat > "$pkgdir/usr/bin/dirbuster" <<EOF
#!/bin/sh
cd /opt/dirbuster
exec java -jar DirBuster-${pkgver/_/-}.jar "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/dirbuster"
}
