# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='openvas-scanner'
pkgver='4.0.4'
pkgrel=1
groups=('blackarch' 'blackarch-scanner')
pkgdesc='The OpenVAS scanning Daemon.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.openvas.org/"
license=('GPL')
depends=('gnutls2' 'openvas-libraries')
makedepends=('cmake' 'pkg-config')
optdepends=('nmap: for nmap based port scanning support'
            'pnscan: for pnscan based port scanning support'
            'ike-scan: for IPsec VPN scanning support'
            'nikto: for web service scanning support'
            'w3af: for web application auditing support'
            'amap: for identifying services')
options=('!makeflags !buildflags')
# The file downloaded is determined by the `/1791/` part. Changing `pkgver`
#  does not change the file downloaded so we hard code the version number to
#  avoid confusion.
source=('http://wald.intevation.org/frs/download.php/1791/openvas-scanner-4.0.4.tar.gz'
        'openvas-scanner.service')
sha1sums=('768613d2a664c5670a266026c624bb8ca35478fe'
          '5122a5495bd8260897b9c6682c0e153f3af15941')

build() {
  cd "$srcdir/openvas-scanner-$pkgver"

  export PKG_CONFIG_PATH=/usr/gnutls2/lib/pkgconfig/:$PKG_CONFIG_PATH

  CFLAGS="-I/usr/gnutls2/include" LDFLAGS="-L/usr/gnutls2/lib"

  cmake -D CMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_RPATH="/usr/gnutls2/lib/" \
    -DCMAKE_INSTALL_RPATH_USE_LINK_PATH=TRUE -DSBINDIR=/usr/bin \
    -DCMAKE_BUILD_WITH_INSTALL_RPATH=TRUE -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var -DLIBDIR=/usr/lib .

  make
}

package() {
  cd "$srcdir/openvas-scanner-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -Dm644 "$srcdir/openvas-scanner.service" \
    "$pkgdir/usr/lib/systemd/system/openvas-scanner.service"
}
