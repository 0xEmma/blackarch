# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='crunch'
pkgver='3.4'
pkgrel=4
groups=('blackarch' 'blackarch-automation' 'blackarch-cracker')
pkgdesc="A wordlist generator for all combinations/permutations of a given character set"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://sourceforge.net/projects/crunch-wordlist/'
license=('GPL')
depends=('glibc')
source=("http://downloads.sourceforge.net/crunch-wordlist/crunch-$pkgver.tgz")
sha1sums=('2e73faa0f8f8a9a761c1f191e201d0be76f74491')

build() {
  cd "$srcdir/crunch-$pkgver"
  make
}

package() {
  cd "$srcdir/crunch-$pkgver"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/crunch"
  install -dm755 "$pkgdir/usr/share/man/man1"

  # Bin.
  install -m755 crunch "$pkgdir/usr/bin/crunch"

  # Data.
  install -m644 charset.lst "$pkgdir/usr/share/crunch/charset.lst"

  # Man.
  install -m644 crunch.1 "$pkgdir/usr/share/man/man1/crunch.1"
}
