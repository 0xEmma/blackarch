# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=openvas-client
pkgver=3.0.3
pkgrel=7
groups=('blackarch' 'blackarch-scanner')
pkgdesc="A lightweight client. Although it is deprecated by upstream, it is still preferred by some. Please do not mark this as out-dated."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.openvas.org/"
license=('GPL')
depends=('gtk2' 'openvas-libraries')
options=('!makeflags')
# The file downloaded is determined by the `/845/` part. Changing `pkgver`
#  does not change the file downloaded so we hard code the version number to
#  avoid confusion.
source=('http://wald.intevation.org/frs/download.php/845/openvas-client-3.0.3.tar.gz'
        'openvas-client-3.0.3.diff')
sha1sums=('347e2f0a77609892e9f8ee6f57d6709a2746e6ae'
          '8dafaadd35ea0e52dc9a05721b5907ceb23868fa')

prepare() {
  cd "$srcdir/openvas-client-$pkgver"

  patch -p1 < "$srcdir/openvas-client-3.0.3.diff"
}

build() {
  cd "$srcdir/openvas-client-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/openvas-client-$pkgver"

  make DESTDIR="$pkgdir/" install
}
