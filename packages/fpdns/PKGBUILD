# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=fpdns
pkgver=0.9.3
pkgrel=6
pkgdesc="Program that remotely determines DNS server versions"
url="http://code.google.com/p/fpdns/"
depends=('perl')
license=('BSD')
options=('!emptydirs')
arch=('any')
groups=('blackarch' 'blackarch-fingerprint')
source=("http://fpdns.googlecode.com/files/Net-DNS-Fingerprint-$pkgver.tar.gz")
sha1sums=('0b4b4451d6547c21dbfa3085d61c867ae1d6dbc9')

build() {
  cd "$srcdir/Net-DNS-Fingerprint-$pkgver"

  eval $(perl -V:archname)

  /usr/bin/perl Makefile.PL \
    INSTALLARCHLIB=/usr/lib/perl5/current/$archname \
    INSTALLSITELIB=/usr/lib/perl5/site_perl/current \
    INSTALLSITEARCH=/usr/lib/perl5/site_perl/current/$archname

  /usr/bin/make
}

package() {
  cd "$srcdir/Net-DNS-Fingerprint-$pkgver"

  install -Dm755 "$srcdir/Net-DNS-Fingerprint-$pkgver/apps/fpdns" \
    "$pkgdir/usr/bin/fpdns"
  install -Dm444 \
    "$srcdir/Net-DNS-Fingerprint-$pkgver/blib/lib/Net/DNS/Fingerprint.pm" \
    "$pkgdir/usr/lib/perl5/site_perl/Net/DNS/Fingerprint.pm"
  install -Dm644 "${srcdir}/Net-DNS-Fingerprint-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/LICENSE"
}
