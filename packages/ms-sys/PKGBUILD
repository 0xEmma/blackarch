# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ms-sys'
pkgver='2.4.0'
pkgrel=1
pkgdesc='A tool to write Win9x-.. master boot records (mbr) under linux - RTM!'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://ms-sys.sourceforge.net/'
license=('GPL2')
groups=('blackarch' 'blackarch-backdoor' 'blackarch-binary' 'blackarch-forensic')
source=("http://downloads.sourceforge.net/sourceforge/ms-sys/ms-sys-${pkgver}.tar.gz")
sha1sums=('09da17de95f9c6e8bd400432898fd99053b7408e')

prepare() {
  cd "$srcdir/ms-sys-$pkgver"

  mkdir -p "$pkgdir/usr/{bin,share/man/man1}"
  sed -i "s|MANDIR =.*|MANDIR = /usr/share/man|g" Makefile
}

build() {
  cd "$srcdir/ms-sys-$pkgver"

  make PREFIX="/usr"
}

package() {
  cd "$srcdir/ms-sys-$pkgver"

  make PREFIX="/usr" DESTDIR="$pkgdir" install

  install -Dm644 "COPYING" "$pkgdir/usr/share/licenses/ms-sys/COPYING"
  install -Dm644 "README" "$pkgdir/usr/share/doc/ms-sys/README"
  install -Dm644 "FAQ" "$pkgdir/usr/share/doc/ms-sys/FAQ"
}
