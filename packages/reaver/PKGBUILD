# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='reaver'
pkgver='1.4'
pkgrel=8
groups=('blackarch' 'blackarch-cracker' 'blackarch-wireless')
pkgdesc='Implements a brute force attack against wifi protected setup WPSregistrar PINs in order to recover WPA/WPA2 passphrases.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://code.google.com/p/reaver-wps'
license=('GPL2')
depends=('libpcap-1.4' 'sqlite')
# libpcap in makedepends is necessary to fool configure
makedepends=('subversion' 'libpcap')
source=('svn+http://reaver-wps.googlecode.com/svn/trunk/')
sha1sums=('SKIP')

build() {
  cd "$srcdir/trunk/src"

  LDFLAGS=-lpcap-1.4 ./configure --prefix=/usr
  sed -i '/^LDFLAGS/s/-lpcap/-lpcap-1.4/' Makefile
  make
}

# We can't install with the Makefile. It tries to clean up the src directory,
#  which makepkg forbids.
package() {
  cd "$srcdir/trunk/src"

  install -Dm755 -t "$pkgdir/usr/bin" reaver wash
  install -Dm644 reaver.db "$pkgdir/etc"

  install -Dm644 -t "$pkgdir/usr/share/doc/reaver/" ../docs/README \
    ../docs/README.REAVER ../docs/README.WASH
  install -Dm644 ../docs/reaver.1.gz "$pkgdir/usr/share/man/man1/reaver.1.gz"
  install -Dm644 ../docs/LICENSE "$pkgdir/usr/share/licenses/reaver/LICENSE"
}
