# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='patator'
pkgver='0.5'
pkgrel=2
groups=('blackarch' 'blackarch-cracker')
pkgdesc="A multi-purpose bruteforcer"
arch=('any')
url='http://code.google.com/p/patator/'
license=('GPL2')
depends=('python2' 'python2-paramiko' 'python2-pycurl' 'openldap' 'impacket'
         'mysql-python' 'python2-psycopg2' 'python2-crypto' 'python2-pydns'
         'python2-pysnmp' 'python2-ipy' 'java-environment' 'unzip')
source=("http://patator.googlecode.com/files/patator_v$pkgver.py")
sha1sums=('87a2e95f9873daa52d5c02a1b0d4a01880634a12')

build() {
  sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' -i "$srcdir/patator_v$pkgver.py"
}

package() {
  install -D -m 0755 "$srcdir/patator_v$pkgver.py" "$pkgdir/usr/bin/patator"

  _modules=(
    'ftp_login'
    'ssh_login'
    'telnet_login'
    'smtp_login'
    'smtp_vrfy'
    'smtp_rcpt'
    'finger_lookup'
    'http_fuzz'
    'pop_login'
    'pop_passd'
    'imap_login'
    'ldap_login'
    'smb_login'
    'smb_lookupsid'
    'vmauthd_login'
    'mssql_login'
    'oracle_login'
    'mysql_login'
    'pgsql_login'
    'vnc_login'
    'dns_forward'
    'dns_reverse'
    'snmp_login'
    'unzip_pass'
    'keystore_pass'
  )
  for module in ${_modules[@]}; do
    ln -s /usr/bin/patator "$pkgdir/usr/bin/$module"
  done
}
