# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=zaproxy
pkgver=2.2.2
pkgrel=3
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-webapp')
pkgdesc="A local intercepting proxy with integrated penetration testing tool for finding vulnerabilities in web applications"
arch=('any')
url="http://code.google.com/p/zaproxy/"
license=('Apache')
depends=('java-environment' 'bash')
source=("http://zaproxy.googlecode.com/files/ZAP_${pkgver}_Linux.tar.gz"
        'zaproxy.diff')
sha1sums=('12d101d2a9b35edd2574f8e7e140da5bfc0a726d'
          'fd8a394795820e00d855166c8cc3021018ada3be')

prepare() {
  cd "$srcdir/ZAP_$pkgver"

  # workaround for version parse error when _JAVA_OPTIONS is set
  # NOTE: this will be fixed in next stable release
  patch -p0 < "$srcdir/zaproxy.diff"
}

package() {
  cd "$srcdir/ZAP_$pkgver"

  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/zaproxy"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/zaproxy"

  cat > "$pkgdir/usr/bin/zaproxy" <<EOF
#!/bin/sh
exec /usr/share/zaproxy/zap.sh
EOF
  chmod +x "$pkgdir/usr/bin/zaproxy"
}
