 # This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='androguard'
pkgver='1.9'
pkgrel=2
groups=('blackarch' 'blackarch-binary' 'blackarch-disassembler'
        'blackarch-malware')
pkgdesc='Reverse engineering, Malware and goodware analysis of Android applications and more.'
arch=('any')
url='https://github.com/androguard/androguard'
license=('Apache')
depends=('python2' 'python2-lxml' 'sparsehash' 'muparser' 'snappy' 'bzip2'
         'zlib' 'python2-pydot' 'python2-magic' 'python2-networkx')
makedepends=('python2-setuptools')
optdepends=(
  'ipython2: interactive analysis support'
  'psyco2-svn: acceleration support'
  'python2-pygments'
)
source=("https://github.com/androguard/androguard/archive/${pkgver}.tar.gz")
sha1sums=('1c2a594b604e33ce3fe9f5b62c2b5b515ce84d8a')

package() {
  cd "$srcdir/androguard-$pkgver"

  install -Dm644 -t "$pkgdir/usr/share/doc/androguard/" README.txt CHANGELOG
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/androguard/COPYING"

  python2 setup.py install --root="$pkgdir" --optimize=1

  for bin in $pkgdir/usr/bin/*
  do
    mv $bin `echo $bin | sed 's/.py//'`
  done
}
