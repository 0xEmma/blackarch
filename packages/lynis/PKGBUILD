# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='lynis'
pkgver='1.4.5'
pkgrel=1
groups=('blackarch' 'blackarch-scanner')
pkgdesc="An auditing tool for Unix (specialists)"
url='http://www.rootkit.nl/projects/lynis.html'
license=('GPL3')
arch=('any')
depends=('bash')
source=("http://cisofy.com/files/lynis-$pkgver.tar.gz")
sha1sums=('c2586439b5d80652c86d44df87e3eb49a06dd31c')

package() {
  cd "$srcdir/lynis-$pkgver"

  # Profile.
  install -Dm 644 default.prf "$pkgdir/etc/lynis/default.prf"

  # Bin.
  install -Dm 755 lynis "$pkgdir/usr/bin/lynis"

  # Install plugins, include, db.
  install -d "$pkgdir/usr/share/lynis"
  cp -a --no-preserve=ownership db include plugins "$pkgdir/usr/share/lynis"
  chmod -R u=rwX,go=rX "$pkgdir/usr/share/lynis"

  # Install misc files.
  install -m644 -t "$pkgdir/usr/share/lynis" README INSTALL CHANGELOG FAQ

  # Install manpage.
  install -Dm644 lynis.8 "$pkgdir/usr/share/man/man8/lynis.8"
}
