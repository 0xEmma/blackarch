# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='bulk-extractor'
pkgver='1.3.1'
pkgrel=8
groups=('blackarch' 'blackarch-forensic' 'blackarch-misc')
pkgdesc="Bulk Email and URL extraction tool"
arch=('i686' 'x86_64')
url='https://github.com/simsong/bulk_extractor'
license=('GPL')
depends=('tre' 'java-environment' 'openssl' 'exiv2' 'afflib' 'libewf')
source=(http://digitalcorpora.org/downloads/bulk_extractor/bulk_extractor-${pkgver}.tar.gz)
sha1sums=('91f9adcfad206022fb4eb803ba680044a7ff5021')

prepare() {
  cd "$srcdir/bulk_extractor-$pkgver"

  sed -i 's|`dirname $0`|/usr/share/bulk-extractor|' java_gui/BEViewer
}

build() {
  cd "$srcdir/bulk_extractor-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc
  make
  make -C java_gui
}

package() {
  cd "$srcdir/bulk_extractor-$pkgver"

  mkdir -p "$pkgdir/usr/share/bulk-extractor"

  make "DESTDIR=$pkgdir" install

  install -m644 java_gui/BEViewer.jar "$pkgdir/usr/share/bulk-extractor"
  install -m755 java_gui/BEViewer "$pkgdir/usr/bin"
}
