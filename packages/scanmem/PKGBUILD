# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='scanmem'
pkgver='0.15.1'
pkgrel=2
groups=('blackarch' 'blackarch-reversing')
pkgdesc='A utility used to locate the address of a variable in an executing process.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://code.google.com/p/scanmem/'
license=('GPL3')
depends=('python2' 'pygtk')
makedepends=('intltool')
optdepends=('gksu: for the GameConqueror GUI.')
source=("https://github.com/scanmem/scanmem/archive/v${pkgver}.tar.gz")
sha1sums=('c20d494ac9ed37540978658e4385447fd0a0e9c3')

build() {
  cd "$srcdir/scanmem-$pkgver"

  ./autogen.sh

  ./configure --enable-gui --prefix=/usr

  make
}

package() {
  cd "$srcdir/scanmem-$pkgver"

  make -s DESTDIR="$pkgdir" install

  # Fix Python shebang lines.
  sed 's|python|python2|g' -i "$pkgdir/usr/bin/gameconqueror"
  sed 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' -i \
      "$pkgdir/usr/share/gameconqueror/"{backend.py,GameConqueror.py,hexview.py}
}

