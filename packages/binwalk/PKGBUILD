# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='binwalk'
pkgver='2.0.1'
pkgrel=1
groups=('blackarch' 'blackarch-firmware')
pkgdesc='A tool for searching a given binary image for embedded files.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://binwalk.org"
license=('MIT')
optdepends=('firmware-mod-kit: Firmware extract rules helper')
depends=('python2' 'python2-magic' 'python2-pyqtgraph' 'python2-opengl'
         'python2-pydeep')
makedepends=('python2-setuptools')
source=("https://github.com/devttys0/binwalk/archive/v${pkgver}.tar.gz"
        'binwalk.patch')
sha1sums=('b0ec783cbf72db358c5118a3052fc1ccf446d8f3'
          '42f5aac1b50e017821c662bc643a4909d4daff8d')

build() {
  cd "$srcdir/binwalk-$pkgver"

  python2 setup.py build

  ./configure --prefix=/usr

  patch -i "$srcdir/binwalk.patch"

  make
}

package() {
  cd "$srcdir/binwalk-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/binwalk/LICENSE"
}
