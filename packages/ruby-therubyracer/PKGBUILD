# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=ruby-therubyracer
_gemname=${pkgname#ruby-}
pkgver=0.12.0
pkgrel=3
pkgdesc="Call JavaScript code and manipulate JavaScript objects from Ruby. Call Ruby code and manipulate Ruby objects from JavaScript."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/cowboyd/therubyracer"
license=('MIT')
depends=('ruby-libv8>=3.11.8.12' 'ruby-ref')
makedepends=('rubygems')
options=('!distcc')
source=("http://gems.rubyforge.org/gems/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
sha256sums=('06a711c23c6fcabc60a38413d6e08023b4717b725c0f19b3fd3d6161f714b3a0')

package() {
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"
  if [[ $CARCH == arm* ]] ; then
    gem install --no-user-install --ignore-dependencies --no-rdoc --no-ri -i "$pkgdir$_gemdir" therubyracer-$pkgver.gem
  else
    gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" therubyracer-$pkgver.gem
  fi
}
