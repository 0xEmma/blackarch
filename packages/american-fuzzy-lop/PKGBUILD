# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='american-fuzzy-lop'
pkgver='1.58b'
pkgrel=1
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-binary')
pkgdesc='A practical, instrumentation-driven fuzzer for binary formats.'
arch=('i686' 'x86_64')
url='https://code.google.com/p/american-fuzzy-lop/'
license=('apache')
depends=('glibc')
source=("http://lcamtuf.coredump.cx/afl/releases/afl-${pkgver}.tgz")
sha1sums=('e7b37fd3e6fe916bc2a4652200ac8d13c51234b8')

prepare() {
 cd "$srcdir/afl-$pkgver"

 sed -i 's|/usr/local|/usr|g' Makefile
}

build() {
  cd "$srcdir/afl-$pkgver"

  make PREFIX="/usr"
}

package() {
  cd "$srcdir/afl-$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/lib/afl"
  mkdir -p "$pkgdir/usr/share/doc/afl"

  make DESTDIR="$pkgdir" install

  cp -a docs/* "$pkgdir/usr/share/doc/afl"
}
