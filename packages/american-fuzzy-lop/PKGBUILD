# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='american-fuzzy-lop'
pkgver='1.39b'
pkgrel=1
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-binary')
pkgdesc='A practical, instrumentation-driven fuzzer for binary formats.'
arch=('i686' 'x86_64')
url='https://code.google.com/p/american-fuzzy-lop/'
license=('apache')
if [[ $CARCH == 'x86_64' ]]; then
  depends=('lib32-glibc')
  makedepends=('gcc-multilib')
else
  depends=('glibc')
fi
source=("http://lcamtuf.coredump.cx/afl/releases/afl-$pkgver.tgz")
sha1sums=('f36206009512af29d44a3b3bd5dec405db2b30ab')

prepare() {
 cd "$srcdir/afl-$pkgver"

 sed -i 's|/usr/local|/usr|g' Makefile
}

build() {
  cd "$srcdir/afl-$pkgver"

  make PREFIX="/usr"
}

package() {
  cd "$srcdir/afl-$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/lib/afl"
  mkdir -p "$pkgdir/usr/share/doc/afl"

  make DESTDIR="$pkgdir" install

  cp -a docs/* "$pkgdir/usr/share/doc/afl"
}
