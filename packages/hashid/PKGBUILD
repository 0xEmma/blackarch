# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='hashid'
pkgver='75.7aaa3ae'
pkgrel=1
groups=('blackarch' 'blackarch-crypto')
pkgdesc="Software to identify the different types of hashes used to encrypt data"
url="https://github.com/psypanda/hashID"
license=('GPL3')
arch=('any')
depends=('python')
makedepends=('git')
source=("git+https://github.com/psypanda/hashID")
sha1sums=('SKIP')

pkgver() {
  cd hashID
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/hashID"

  # Base directories.
  install -dm755 "$pkgdir/usr/share/doc/hashid"
  install -dm755 "$pkgdir/usr/bin"

  install -m644 README.md "$pkgdir/usr/share/doc/hashid"
  install -m755 python/hashid.py "$pkgdir/usr/bin/hashid"
}
