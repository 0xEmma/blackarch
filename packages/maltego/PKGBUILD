# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='maltego'
pkgver='3.3.0'
pkgrel=7
groups=('blackarch' 'blackarch-forensic' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='An open source intelligence and forensics application, enabling to easily gather information about DNS, domains, IP addresses, websites, persons, etc.'
arch=('any')
url='http://www.paterva.com/web5'
license=('GPL')
depends=('java-environment' 'giflib' 'python2')
source=('https://www.paterva.com/malv32/community/maltego-radium-CE.community-2012-12-20.zip'
        'http://www.paterva.com/malv3/303/M3GuideGUI.pdf')
sha1sums=('960707952ade6a07f87dbee6a76bcc3a2c804739'
          'b3d496acaa2fbd5a0fd0409aafc0af05280db736')

package() {
  cd "$srcdir/v3.0"

  install -dm755 "$pkgdir/usr/share/maltego"
  install -dm755 "$pkgdir/usr/share/doc/maltego"
  install -dm755 "$pkgdir/usr/bin"

  cp -aR bin etc groovy ide java maltego maltego-core-platform maltego-ui \
    platform "$pkgdir/usr/share/maltego"

  install -Dm644 "$srcdir/M3GuideGUI.pdf" \
    "$pkgdir/usr/share/doc/maltego/M3GuideGUI.pdf"

  cat > "$pkgdir/usr/bin/maltego" << EOF
#!/bin/sh
cd /usr/share/maltego/bin
exec ./maltego "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/maltego"
}
