# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='maltego'
pkgver='3.4.0.5004'
pkgrel=1
groups=('blackarch' 'blackarch-forensic' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='An open source intelligence and forensics application, enabling to easily gather information about DNS, domains, IP addresses, websites, persons, etc.'
arch=('any')
url='http://www.paterva.com/web5'
license=('GPL')
depends=('java-environment' 'giflib' 'python2')
source=("http://www.paterva.com/malv34/community/MaltegoCE.v${pkgver}.zip"
        'http://www.paterva.com/malv3/303/M3GuideGUI.pdf')
sha1sums=('e2a29f01b05ce3ced56c09a9a847659070f97746'
          'b3d496acaa2fbd5a0fd0409aafc0af05280db736')

package() {
  cd "$srcdir/MaltegoTungstenCE$pkgver"

  mkdir -p "$pkgdir/usr/share/maltego"
  mkdir -p "$pkgdir/usr/share/doc/maltego"
  mkdir -p "$pkgdir/usr/bin"

  cp -aR bin etc groovy ide java maltego maltego-core-platform maltego-ui \
    platform "$pkgdir/usr/share/maltego"

  install -Dm644 "$srcdir/M3GuideGUI.pdf" \
    "$pkgdir/usr/share/doc/maltego/M3GuideGUI.pdf"

  cat > "$pkgdir/usr/bin/maltego" << EOF
#!/bin/sh
cd /usr/share/maltego/bin
exec ./maltego "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/maltego"
}
