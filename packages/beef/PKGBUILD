# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=beef
pkgver=0.4.4.9.104.g3494542
pkgrel=1
groups=('blackarch' 'blackarch-exploitation')
pkgdesc="BeEF, the Browser Exploitation Framework - it is a penetration testing tool that focuses on the web browser"
arch=('any')
url='http://beefproject.com/'
install=beef.install
license=('Apache')
makedepends=('git')
depends=('ruby1.9' 'ruby1.9-bundler' 'sqlite' 'python2')
source=('git+http://github.com/beefproject/beef.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/beef"
  git describe | sed 's/^v//;s/-/./g;s/_/./g;s/^beef.//'
}

package() {
  cd "$srcdir/beef"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"

  # The .git/ overhead is tiny and this allows users to update with git.
  git clone "$srcdir/beef" "$pkgdir/usr/share/beef"

  rm "$pkgdir/usr/share/beef/install"

  cat > "$pkgdir/usr/bin/beef" <<EOF
#!/bin/sh
cd /usr/share/beef
ruby-1.9 beef "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/beef"
}
