# TODO: make package function
pkgname='recstudio'
pkgver=4.0_20130717
pkgrel=1
groups=(blackarch blackarch-reversing)
pkgdesc='Cross platform interactive decompiler.'
arch=(i686 x86_64)
url='http://www.backerstreet.com/rec/rec.htm'
license=(custom)
depends=(gtk2)
[[ $CARCH == x86_64 ]] && depends=(lib32-gtk2)

source=(http://www.backerstreet.com/rec/RecStudioLinux.tgz
        recstudio
        recstudio.desktop
        recstudio.png
        LICENSE)
# This program's method of distribution is not sane.
# Expect this md5sum to change randomly.
md5sums=(69a1c516f8dc2eb5c20d58955ad07e43
         5d6a0bc5ef2a9372b33c78860e776f37
         8d7e21187611fe192789663e9b9ca92b
         b0e43f834bd7405a36f29c0213413ba3
         639152ef5ee75ce32fd1df8a72968961)
options=(!strip)

package() {
  cd $srcdir

  # Create directories.
  install -dm 755 $pkgdir/opt/recstudio/{bin,support}
  install -dm 755 $pkgdir/opt/recstudio/support/{Prototypes,win32api}

  # Copy files.
  install -Dm 755 bin/* $pkgdir/opt/recstudio/bin
  install -Dm 644 support/{*.rse,*.txt,*h} $pkgdir/opt/recstudio/support
  install -Dm 644 support/Prototypes/* $pkgdir/opt/recstudio/support/Prototypes
  install -Dm 644 support/win32api/* $pkgdir/opt/recstudio/support/win32api

  install -Dm 755 recstudio $pkgdir/usr/bin/recstudio
  install -Dm 644 recstudio.desktop $pkgdir/usr/share/applications/recstudio.desktop
  install -Dm 644 recstudio.png $pkgdir/usr/share/pixmaps/recstudio.png
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/recstudio/LICENSE
}

