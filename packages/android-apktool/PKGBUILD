# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='android-apktool'
pkgver='1.5.2'
pkgrel=6
pkgdesc='A tool for reengineering Android apk files.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
groups=('blackarch' 'blackarch-reversing' 'blackarch-disassembler')
url='http://forum.xda-developers.com/showthread.php?t=1755243'
license=('Apache')
depends=('java-environment' 'android-sdk-build-tools')
source=("http://android-apktool.googlecode.com/files/apktool${pkgver}.tar.bz2"
        'http://android-apktool.googlecode.com/files/apktool-install-linux-r04-brut1.tar.bz2')
sha1sums=('2dd828cf79467730c7406aa918f1da1bd21aaec8'
          '0d3daa89fd77497ebff04c2c2dce07a92fb94048')

prepare() {
  cd "$srcdir"

  sed -i 's|libdir=.*progdir|libdir="/usr/share/android-apktool|' apktool
}

package() {
  cd "$srcdir"

  install -Dm755 "$srcdir/apktool" "$pkgdir/usr/bin/apktool"
  install -Dm644 "$srcdir/apktool$pkgver/apktool.jar" \
    "$pkgdir/usr/share/android-apktool/apktool.jar"
}
