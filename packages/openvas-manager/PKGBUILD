# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='openvas-manager'
pkgver='5.0.2'
pkgrel=2
groups=('blackarch' 'blackarch-scanner')
pkgdesc="The OpenVAS Manager is a layer between the OpenVAS Scanner and various client applications"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.openvas.org/"
license=('GPL')
depends=('gnutls2' 'libssh' 'glib2' 'sqlite3' 'doxygen' 'openvas-libraries')
makedepends=('cmake' 'pkg-config' 'xmltoman')
optdepends=('libxslt: for generating HTML reports')
options=('!makeflags !buildflags')
conflicts=('openvas-administrator')
# The file downloaded is determined by the `/1421/` part. Changing `pkgver`
#  does not change the file downloaded so we hard code the version number to
#  avoid confusion.
source=('http://wald.intevation.org/frs/download.php/1667/openvas-manager-5.0.2.tar.gz'
        'openvas-manager.service')
sha1sums=('ede1669732f20538c33fd6d5c60ed9252724eda8'
          '7085514627f41296dc9eb8d9964f38f29b004b37')

prepare() {
  cd "$srcdir/openvas-manager-$pkgver"

  find . -name 'CMakeLists.txt' -exec sed -i 's/-Werror//' {} \;
}

build() {
  cd "$srcdir/openvas-manager-$pkgver"

  export PKG_CONFIG_PATH=/usr/gnutls2/lib/pkgconfig/:$PKG_CONFIG_PATH
  CFLAGS="-I/usr/gnutls2/include" LDFLAGS="-L/usr/gnutls2/lib"

  cmake -D CMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_RPATH="/usr/gnutls2/lib/" -DCMAKE_INSTALL_RPATH_USE_LINK_PATH=TRUE -DSBINDIR=/usr/bin \
  -DCMAKE_BUILD_WITH_INSTALL_RPATH=TRUE -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var -DLIBDIR=/usr/lib .
  make
}

package() {
  cd "$srcdir/openvas-manager-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 "$srcdir/openvas-manager.service" "$pkgdir/usr/lib/systemd/system/openvas-manager.service"
}
