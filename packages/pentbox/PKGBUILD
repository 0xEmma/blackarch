# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=pentbox
pkgver=1.5
pkgrel=7
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-scanner')
pkgdesc="A security suite that packs security and stability testing oriented tools for networks and systems"
arch=('any')
url='http://www.pentbox.net'
license=('GPL3')
source=("http://downloads.sourceforge.net/project/pentbox/$pkgver/pentbox-$pkgver.tar")
depends=('ruby1.9')
sha1sums=('ef8ecf884e024713291fe6e2f329506c364b1b63')
options=('!strip')

package() {
  cd "$srcdir/pentbox-$pkgver"

  # Base directories.
  install -dm755 "$pkgdir/usr/share/pentbox"
  install -dm755 "$pkgdir/usr/bin"

  # There might be a better way to install this. I don't know much about Ruby
  #  though.
  cp --no-preserve=ownership -R * "$pkgdir/usr/share/pentbox"

  # Joint script.
  cat > "$pkgdir/usr/bin/pentbox" <<EOF
#!/bin/sh
ruby-1.9 /usr/share/pentbox/pentbox.rb "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/pentbox"
}
