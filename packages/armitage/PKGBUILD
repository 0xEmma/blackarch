pkgname=armitage
pkgver=2013.08.21
groups=('blackarch' 'blackarch-exploitation')
pkgrel=2
epoch=2
pkgdesc="A graphical cyber attack management tool for Metasploit"
url='http://www.fastandeasyhacking.com/'
arch=('any')
license=('GPL')
depends=('metasploit' 'java-environment')
makedepends=('apache-ant' 'zip' 'unzip' 'subversion')
provides=('armitage')
install=armitage.install
source=('armitage.install'
        "http://www.fastandeasyhacking.com/download/armitage$(sed 's/\.//g' <<< $pkgver).tgz")
md5sums=('5234326f9ff41ff8694cb35a557af895'
         '05426fdb982dc47818012f46e67b9fe0')

prepare() {
  cd "$srcdir/trunk"
  sed -i 's|/usr/share/java/apache-ant/bin/||g' package.sh
}

build() {
  cd "$srcdir/trunk"
  bash package.sh
}

package() {
  # Base directories.
  install -dm755 "$pkgdir/usr/share"
  install -dm755 "$pkgdir/usr/bin"

  # Joint script.
  cat > "$pkgdir/usr/bin/armitage" <<\EOF
#!/bin/sh
java -jar /usr/share/armitage/armitage.jar "$@"
EOF
  chmod +x "$pkgdir/usr/bin/armitage" 

  # Everything else.
  tar -xzf "$srcdir/trunk/armitage.tgz" -C "$pkgdir/usr/share"
}
