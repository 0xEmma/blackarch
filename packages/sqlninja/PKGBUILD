# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='sqlninja'
pkgver='0.2.6_r1'
pkgrel=2
groups=('blackarch' 'blackarch-exploitation' 'blackarch-fuzzer' 'blackarch-webapp')
pkgdesc="A tool targeted to exploit SQL Injection vulnerabilities on a web application that uses Microsoft SQL Server as its back-end"
arch=('any')
url='http://sqlninja.sourceforge.net/'
license=('GPL2')
depends=('perl' 'perl-netpacket' 'perl-net-pcap' 'perl-net-dns' 'perl-net-rawip' 'perl-io-socket-ssl')
optdepends=('metasploit: to use the metasploit attack mode'
            'tigervnc: a VNC client, vncviewer, to use the VNC payload')
conflicts=('sqlninja-svn')
source=("http://downloads.sourceforge.net/sourceforge/sqlninja/sqlninja-${pkgver/_/-}.tgz")
sha1sums=('b2ed4e0daea858b10190a3838a280faecee98fde')

prepare() {
  cd "$srcdir/sqlninja-${pkgver/_/-}"

  # TODO: Is this still relevant?
  # Check if metasploit-svn or metasploit stable is installed (-svn takes precedence)
  # Apply Archlinux metasploit home directory to sqlninja.conf.
  if [[ -d /usr/src/metasploit ]] ; then
    sed -i 's|/home/sqlninja/trunk/|/usr/src/metasploit|g' sqlninja.conf.example
  else
    sed -i 's|/home/sqlninja/trunk/|/usr/share/metasploit|g' sqlninja.conf.example
  fi
}

package() {
  cd "$srcdir/sqlninja-${pkgver/_/-}"

  # Make base directories.
  mkdir -p "$pkgdir/usr/share/sqlninja"
  mkdir -p "$pkgdir/usr/share/doc/sqlninja"
  mkdir -p "$pkgdir/usr/share/licenses/sqlninja"
  mkdir -p "$pkgdir/usr/bin"

  install -m644 ChangeLog README sqlninja-howto.html "$pkgdir/usr/share/doc/sqlninja"

  cp -pR apps scripts sources sqlninja sqlninja.conf.example "$pkgdir/usr/share/sqlninja"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/sqlninja/LICENSE"

  # Make joint script.
  cat > "$pkgdir/usr/bin/sqlninja" <<EOF
#!/bin/bash
cd /usr/share/sqlninja
exec ./sqlninja "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/sqlninja"
}
