# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=tcpxtract
pkgver=1.0.1
pkgrel=3
pkgdesc="tcpxtract is a tool for extracting files from network traffic"
url="http://tcpxtract.sourceforge.net"
depends=('libpcap')
license=('GPL')
groups=('blackarch' 'blackarch-misc')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
source=(http://downloads.sourceforge.net/tcpxtract/tcpxtract-$pkgver.tar.gz
        conf_default_path.patch)
md5sums=('1d8fe172d4590c33a157798e08dc0ccd'
         'c7964f1e9c1315ae2d2b94c2bd06b4b7')

prepare(){
  cd "$srcdir/tcpxtract-$pkgver"

  # Applying conf_default_path patch
  patch -Np0 -i "$srcdir/conf_default_path.patch"
}

build() {
  cd "$srcdir/tcpxtract-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/tcpxtract-$pkgver"

  make "DESTDIR=$pkgdir" install
}
