# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='xssless'
pkgver='r34.d474fc6'
pkgrel=1
pkgdesc="An automated XSS payload generator written in python."
url="https://github.com/mandatoryprogrammer/xssless"
groups=('blackarch' 'blackarch-webapp')
arch=('any')
license=('GPL2')
depends=('python2' 'python2-beautifulsoup4')
makedepends=('git')
source=("git+https://github.com/mandatoryprogrammer/xssless.git")
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/xssless"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  # TODO: fix this.
  grep -iRl 'python' "$srcdir/xssless" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/xssless"

  # Base directories.
  install -dm755 "$pkgdir/usr/share/xssless"
  install -dm755 "$pkgdir/usr/bin/"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/xssless"

cat > "$pkgdir/usr/bin/xssless" <<EOF
#!/bin/sh
exec python2 /usr/share/xssless/xssless.py "\$@"
EOF
 chmod +x "$pkgdir/usr/bin/xssless"
}
