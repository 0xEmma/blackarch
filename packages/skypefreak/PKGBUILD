# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='skypefreak'
pkgver='15.ea94db5'
pkgrel=1
groups=('blackarch' 'blackarch-forensic')
pkgdesc='A Cross Platform Forensic Framework for Skype.'
url='http://osandamalith.github.io/SkypeFreak/'
arch=('any')
license=('GPL3')
depends=('python2' 'python2-pysqlite')
source=('git+https://github.com/OsandaMalith/SkypeFreak.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/SkypeFreak"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/SkypeFreak"

  sed -i '1i\#!/usr/bin/env python2' 'source/SkypeFreak.py'
}

package() {
  cd "$srcdir/SkypeFreak"

  install -Dm755 'source/SkypeFreak.py' "$pkgdir/usr/bin/skypefreak"
  install -Dm644 README.md "$pkgdir/usr/share/doc/skypefreak/README.md"
}
