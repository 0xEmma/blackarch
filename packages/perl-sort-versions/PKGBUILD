# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

_author=E/ED/EDAVIS
_perlmod=Sort-Versions
pkgname=perl-sort-versions
pkgver=1.5
pkgrel=4
pkgdesc="Sort::Versions - a perl 5 module for sorting of revision-like numbers"
arch=('any')
url='http://search.cpan.org/~edavis/Sort-Versions/'
license=('GPL' 'PerlArtistic')
depends=('perl')
source=("http://search.cpan.org/CPAN/authors/id/$_author/$_perlmod-$pkgver.tar.gz")
sha1sums=('d2f354b0a6a8254afc9af175c2fb16d85efb73c7')

build(){
  cd "$srcdir/$_perlmod-$pkgver"

  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check(){
  cd "$srcdir/$_perlmod-$pkgver"
  make test
}

package(){
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR="$pkgdir/"

  # remove perllocal.pod and .packlist
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
