pkgname=inetsim
pkgver=1.2.4
pkgrel=1
pkgdesc="A software suite for simulating common internet services in a lab environment, e.g. for analyzing the network behaviour of unknown malware samples"
url="http://www.inetsim.org"
arch=('any')
license=('GPLv3')
depends=('perl' 'perl-net-server' 'perl-ipc-shareable' 'openssl')
install=inetsim.install
source=("http://www.inetsim.org/downloads/inetsim-$pkgver.tar.gz")
md5sums=('1e5d5666b3d5cd750166c0fbbeb735b5')

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # Base directories.
  install -dm755 "$pkgdir/usr/share/inetsim"
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/var/lib/inetsim"
  install -dm755 "$pkgdir/var/log/inetsim"

  cp -a * "$pkgdir/usr/share/inetsim"

  # Logs go to '/var/log/inetsim'.
  rmdir "$pkgdir/usr/share/inetsim/log"
  rmdir "$pkgdir/usr/share/inetsim/report"
  ln -s /var/log/inetsim "$pkgdir/usr/share/inetsim/report"
  ln -s /var/log/inetsim "$pkgdir/usr/share/inetsim/log"

  # Data goes to '/var/lib/inetsim'.
  mv "$pkgdir/usr/share/inetsim/data"/* "$pkgdir/var/lib/inetsim"
  rmdir "$pkgdir/usr/share/inetsim/data"

  ln -s /var/lib/inetsim "$pkgdir/usr/share/inetsim/data"

  # Permissions.
  # This is pretty much copypasted from the upstream-provided `setup.sh`.
  msg2 'setting file permissions...'

  chmod 770 "$pkgdir/var/log/inetsim"
  chmod g+w "$pkgdir/var/lib/inetsim/http/postdata"
  chmod g+w "$pkgdir/var/lib/inetsim/ftp/upload"
  chmod g+w "$pkgdir/var/lib/inetsim/tftp/upload"

  # Joint script.
  cat > "$pkgdir/usr/bin/inetsim" <<EOF
#!/bin/sh
cd /usr/share/inetsim
perl -T inetsim "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/inetsim"
}
