pkgname=inetsim
pkgver=1.2.4
pkgrel=1
pkgdesc="A software suite for simulating common internet services in a lab environment, e.g. for analyzing the network behaviour of unknown malware samples"
url="http://www.inetsim.org"
arch=('any')
license=('GPLv3')
depends=('perl' 'perl-net-server' 'perl-ipc-shareable' 'openssl')
install=inetsim.install
source=("http://www.inetsim.org/downloads/inetsim-$pkgver.tar.gz")
md5sums=('1e5d5666b3d5cd750166c0fbbeb735b5')

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # Base directories.
  install -dm755 "$pkgdir/usr/share/inetsim"
  install -dm755 "$pkgdir/usr/bin/"

  cp -a * "$pkgdir/usr/share/inetsim"

  # Permissions.
  # This is pretty much copypasted from the upstream-provided `setup.sh`.
  msg2 'setting file permissions...'

  # TODO: move out of `/usr/share`.
  cd "$pkgdir/usr/share/inetsim"
  chmod 770 log
  chmod 770 report
  chmod g+w data/http/postdata
  chmod g+w data/ftp/upload
  chmod g+w data/tftp/upload

  # Joint script.
  cat > "$pkgdir/usr/bin/inetsim" <<EOF
#!/bin/sh
cd /usr/share/inetsim
perl -T inetsim "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/inetsim"
}
