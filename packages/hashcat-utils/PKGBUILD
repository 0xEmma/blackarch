# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='hashcat-utils'
pkgver='1.0'
pkgrel=1
groups=('blackarch' 'blackarch-cracker')
pkgdesc='Utilites for Hashcat'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://hashcat.net/wiki/doku.php?id=hashcat_utils'
license=('custom')
makedepends=('p7zip')
source=("http://hashcat.net/tools/hashcat-utils/hashcat-utils-${pkgver}.7z")
sha1sums=('f1581ea0a76dd56c6ff2b98aa8e42c67a5b0276c')

prepare() {
  cd "$srcdir/hashcat-utils-$pkgver"

  sed -i 's/release: posix windows/release: posix/' Makefile
  sed -i '/WINDOWS/d' Makefile
  sed -i '/*.exe/d' Makefile
}

build() {
  cd "$srcdir/hashcat-utils-$pkgver"
  make
}

package() {
  cd "$srcdir/hashcat-utils-$pkgver"

  mkdir -p "$pkgdir/usr/bin"

  for bin in *.bin
  do
    install -m755 "$bin" "$pkgdir/usr/bin/hashcat-${bin%.bin}"
  done
}
