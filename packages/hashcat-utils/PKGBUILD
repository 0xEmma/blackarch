# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='hashcat-utils'
pkgver='0.9'
pkgrel=4
groups=('blackarch' 'blackarch-cracker')
pkgdesc="Utilites for Hashcat"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://hashcat.net/wiki/doku.php?id=hashcat_utils'
license=('custom')
makedepends=('p7zip')
if [[ "$CARCH" == 'x86_64' ]]; then
      source=("http://hashcat.net/files/hashcat-utils-$pkgver-64.7z")
elif [[ "$CARCH" == 'i686' ]]; then
      source=("http://hashcat.net/files/hashcat-utils-$pkgver-32.7z")
fi
sha1sums=('421e878e0d5609546b2c2038b9a0d1e1817e3408')

prepare() {
  cd "$srcdir/hashcat-utils-$pkgver"

  sed -i 's/release: posix windows/release: posix/' Makefile
  sed -i '/WINDOWS/d' Makefile
  sed -i '/*.exe/d' Makefile
}

build() {
  cd "$srcdir/hashcat-utils-$pkgver"
  make
}

package() {
  cd "$srcdir/hashcat-utils-$pkgver"

  # Base directory.
  install -dm755 "$pkgdir/usr/bin"

  # Bin.
  for bin in combinator.bin cutb.bin expander.bin gate.bin hcstatgen.bin \
    len.bin morph.bin permute.bin prepare.bin req.bin rli.bin rli2.bin \
    splitlen.bin
  do
    install -m755 "$bin" "$pkgdir/usr/bin/hashcat-${bin%.bin}"
  done
}
