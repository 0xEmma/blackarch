# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='recon-ng'
pkgver='792.377c606'
pkgrel=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='A full-featured Web Reconnaissance framework written in Python.'
arch=('any')
url='https://bitbucket.org/LaNMaSteR53/recon-ng'
license=('GPL3')
depends=('python2')
makedepends=('git')
source=("git+https://bitbucket.org/LaNMaSteR53/recon-ng.git")
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/recon-ng"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/recon-ng"

  sed -i '1s/env python/env python2/' "recon-ng"
}

package() {
  cd "$srcdir/recon-ng"

  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/recon-ng"

  cp -R * "$pkgdir/usr/share/recon-ng"

  cat >> "$pkgdir/usr/bin/recon-ng" << EOF
#!/bin/sh
cd /usr/share/recon-ng
exec python2 recon-ng
EOF

  chmod +x "$pkgdir/usr/bin/recon-ng"

  cat >> "$pkgdir/usr/bin/recon-cli" << EOF
#!/bin/sh
cd /usr/share/recon-ng
exec python2 recon-cli
EOF

  chmod +x "$pkgdir/usr/bin/recon-cli"

  cat >> "$pkgdir/usr/bin/recon-rpc" << EOF
#!/bin/sh
cd /usr/share/recon-ng
exec python2 recon-rpc
EOF

  chmod +x "$pkgdir/usr/bin/recon-rpc"
}
