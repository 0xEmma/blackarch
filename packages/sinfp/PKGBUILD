# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='sinfp'
_distname='Net-SinFP3'
pkgver='1.21'
pkgrel=2
pkgdesc='A full operating system stack fingerprinting suite.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.networecon.com/tools/sinfp/'
groups=('blackarch' 'blackarch-fingerprint')
license=('PerlArtistic')
depends=('perl' 'perl-class-gomor' 'perl-data-dumper' 'perl-dbd-sqlite'
        'perl-dbi' 'perl-digest-perl-md5' 'perl-net-frame' 'perl-net-libdnet6'
        'perl-net-frame-device' 'perl-net-libdnet' 'perl-net-netmask'
        'perl-net-write' 'perl-parallel-forkmanager' 'perl-net-ipv4addr'
        'perl-net-frame-dump' 'perl-net-frame-layer-ipv6'
        'perl-net-frame-layer-icmp6' 'perl-net-frame-simple'
        'perl-net-frame-layer-sinfp3' 'perl-io-socket-inet6'
        'perl-time-interval')
source=("http://search.cpan.org/CPAN/authors/id/G/GO/GOMOR/Net-SinFP3-$pkgver.tar.gz")
sha1sums=('f2878b908ab2d57313252753c12743b051bec577')

prepare() {
  cd "$srcdir/$_distname-$pkgver"

  perl Makefile.PL
}

build() {
  cd "$srcdir/$_distname-$pkgver"

  make
}

package() {
  cd "$srcdir/$_distname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm755 bin/sinfp3.pl "$pkgdir/usr/share/sinfp3/sinfp3.pl"

  ln -s /usr/share/sinfp3/sinfp3.pl "$pkgdir/usr/bin/sinfp"

  install -D -m644 "$pkgdir/usr/bin/site_perl/sinfp3.db" \
    "$pkgdir/usr/share/sinfp3/sinfp3.db"

  rm -rf "$pkgdir/usr/bin/site_perl"
}
